EventPointerTable(0x21, PointerList)

ALIGN 4
PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Good Good  //This unit group determines the number and position of deployment slots when there is a prep screen
POIN $0 $0 $0
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
//opening: pirate boat present
//turn 1 ep: pirates drop bridge
//turn 3?: lythos boat arrives
//turn 8: win, which means you crash
//flyers move in consistently, stairs spawn reinforcements
//defend your stairs or else!
TurnEventPlayer(0x0,AfterPrepTorches,1)
TurnEventEnemy(0x0, PiratePlanksDrop, 1)
TurnEventPlayer(0x0,Turn2Reinforcements,2)
TurnEventPlayer(0x0, LythosBoatArrives, 3)
TurnEventEnemy(0x0, LythosPlanksDrop, 3)
TurnEventPlayer(0x0,Turn4Reinforcements,4)
TurnEventPlayer(0x0,Turn5Reinforcements,5)
TurnEventPlayer(0x0,Turn6Reinforcements,6)
TurnEventNPC(0x3, EndingScene, 7) //npc phase to stop a turn being tacked on
END_MAIN

CharacterBasedEvents:
END_MAIN

LocationBasedEvents:
#ifdef __DEBUG__
Seize(0x3,EndingScene,14,11)
#endif
END_MAIN

MiscBasedEvents:
CauseGameOverIfLordDies
END_MAIN

Dunno1:
END_MAIN

Dunno2:
END_MAIN

Dunno3:
END_MAIN

Tutorial:
END_MAIN

Good:
UNIT Marth EirikaMasterLord 0x00 Level(1,Ally,0) [14,11] 0x0  0x0 0x0 0x0 [SilverSword, Vulnerary] NoAI
UNIT Sheeda FalcoKnight 0x00 Level(1,Ally,0) [12,7] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT Sheeda FalcoKnight 0x00 Level(1,Ally,0) [13,8] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT Sheeda FalcoKnight 0x00 Level(1,Ally,0) [12,9] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT Sheeda FalcoKnight 0x00 Level(1,Ally,0) [13,10] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT Sheeda FalcoKnight 0x00 Level(1,Ally,0) [12,11] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT Sheeda FalcoKnight 0x00 Level(1,Ally,0) [13,12] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT Sheeda FalcoKnight 0x00 Level(1,Ally,0) [12,13] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT Sheeda FalcoKnight 0x00 Level(1,Ally,0) [13,14] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT Sheeda FalcoKnight 0x00 Level(1,Ally,0) [14,8] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT Sheeda FalcoKnight 0x00 Level(1,Ally,0) [14,14] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT Sheeda FalcoKnight 0x00 Level(1,Ally,0) [15,7] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT Sheeda FalcoKnight 0x00 Level(1,Ally,0) [15,10] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT Sheeda FalcoKnight 0x00 Level(1,Ally,0) [15,13] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT Sheeda FalcoKnight 0x00 Level(1,Ally,0) [14,12] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT

Bad:
UNIT Lythos FalcoKnight 0x00 Level(6,Enemy,1) [18,19] 0x0 0x0 0x0 0x0 [SteelSword] PursueWithoutHeedAI
UNIT Lythos DragonKnight 0x00 Level(6,Enemy,1) [21,16] 0x0 0x0 0x0 0x0 [SteelLance] PursueWithoutHeedAI
UNIT Lythos FalcoKnight 0x00 Level(6,Enemy,1) [29,7] 0x0 0x0 0x0 0x0 [SteelSword] PursueWithoutHeedAI
UNIT Lythos DragonKnight 0x00 Level(6,Enemy,1) [29,10] 0x0 0x0 0x0 0x0 [SteelLance] PursueWithoutHeedAI
UNIT Lythos FalcoKnight 0x00 Level(6,Enemy,1) [29,13] 0x0 0x0 0x0 0x0 [IronLance] PursueWithoutHeedAI
UNIT Lythos DragonKnight 0x00 Level(6,Enemy,1) [29,16] 0x0 0x0 0x0 0x0 [Javelin] PursueWithoutHeedAI
UNIT Lythos FalcoKnight 0x00 Level(3,Enemy,1) [12,0] 0x0 0x0 0x0 0x0 [IronLance] PursueWithoutHeedAI
UNIT Lythos DragonKnight 0x00 Level(3,Enemy,1) [14,0] 0x0 0x0 0x0 0x0 [Javelin] PursueWithoutHeedAI
UNIT

GazackCloneSixGroup:
UNIT PirateGeneric Berserker 0x00 Level(1,Enemy,1) [4,12] DropItem 0x0 0x0 0x0 [Tomahawk, EnergyRing] NeverMoveAI //make this guy an actual boss in the future
UNIT PirateGeneric Pirate 0x00 Level(15,Enemy,1) [7,11] 0x0 0x0 0x0 0x0 [Hammer] PursueWithoutHeedAI
UNIT PirateGeneric Pirate 0x00 Level(15,Enemy,1) [7,12] 0x0 0x0 0x0 0x0 [Halberd] PursueWithoutHeedAI
UNIT PirateGeneric Pirate 0x00 Level(15,Enemy,1) [6,10] 0x0 0x0 0x0 0x0 [SteelAxe] AttackInRangeAI
UNIT PirateGeneric Pirate 0x00 Level(15,Enemy,1) [5,13] 0x0 0x0 0x0 0x0 [SteelAxe] AttackInRangeAI
UNIT PirateGeneric Pirate 0x00 Level(15,Enemy,1) [7,14] 0x0 0x0 0x0 0x0 [SteelAxe] AttackInRangeAI
UNIT PirateGeneric Archer 0x00 Level(15,Enemy,1) [4,13] 0x0 0x0 0x0 0x0 [SteelBow] AttackInRangeAI
UNIT PirateGeneric Thief 0x00 Level(15,Enemy,1) [4,9] 0x0 0x0 0x0 0x0 [SteelSword] StealingThiefAI
UNIT

LythosBoatGroup:
UNIT Louis General 0x00 Level(10,Enemy,0) [24,7] DropItem 0x0 0x0 0x0 [Spear, KnightCrest] NeverMoveAI 
UNIT Lythos General 0x00 Level(6,Enemy,1) [22,10] 0x0 0x0 0x0 0x0 [SilverAxe] NeverMoveAI
UNIT Lythos General 0x00 Level(6,Enemy,1) [22,13] 0x0 0x0 0x0 0x0 [SilverLance] NeverMoveAI
UNIT Lythos Hero 0x00 Level(6,Enemy,1) [25,9] 0x0 0x0 0x0 0x0 [Armourslayer] PursueWithoutHeedAI
UNIT Lythos Hero 0x00 Level(6,Enemy,1) [24,13] 0x0 0x0 0x0 0x0 [Wyrmslayer] PursueWithoutHeedAI
UNIT Lythos Hero 0x00 Level(6,Enemy,1) [25,9] 0x0 0x0 0x0 0x0 [HandAxe] PursueWithoutHeedAI
UNIT Lythos Sniper 0x00 Level(6,Enemy,1) [24,15] 0x0 0x0 0x0 0x0 [Longbow] PursueWithoutHeedAI
UNIT Lythos Bishop 0x00 Level(6,Enemy,1) [23,7] 0x0 0x0 0x0 0x0 [Divine, Physic] HealUnits
UNIT Lythos Sage 0x00 Level(6,Enemy,1) [25,9] 0x0 0x0 0x0 0x0 [Blizzard] PursueWithoutHeedAI
UNIT Lythos Sage 0x00 Level(6,Enemy,1) [24,8] 0x0 0x0 0x0 0x0 [Elfire] GuardTileAI
UNIT

T2Group:
UNIT PirateGeneric Pirate 0x00 Level(15,Enemy,1) [4,10] 0x0 0x0 0x0 0x0 [SteelAxe] PursueWithoutHeedAI
UNIT PirateGeneric Pirate 0x00 Level(15,Enemy,1) [4,14] 0x0 0x0 0x0 0x0 [HandAxe] PursueWithoutHeedAI
UNIT

T3Group:
UNIT PirateGeneric Pirate 0x00 Level(15,Enemy,1) [4,10] 0x0 0x0 0x0 0x0 [Hammer] PursueWithoutHeedAI
UNIT PirateGeneric Pirate 0x00 Level(15,Enemy,1) [4,14] 0x0 0x0 0x0 0x0 [DevilAxe] PursueWithoutHeedAI
UNIT

T4Group:
UNIT PirateGeneric Pirate 0x00 Level(15,Enemy,1) [4,10] DropItem 0x0 0x0 0x0 [DevilAxe] PursueWithoutHeedAI
UNIT PirateGeneric Pirate 0x00 Level(15,Enemy,1) [4,14] 0x0 0x0 0x0 0x0 [SteelAxe] PursueWithoutHeedAI
UNIT Lythos Hero 0x0 Level(4,Enemy,1) [22,15] 0x0 0x0 1 LythosREDA1 [SteelSword] AttackInRangeAI
UNIT Lythos Sage 0x0 Level(4,Enemy,1) [22,15] 0x0 0x0 1 LythosREDA2 [Elfire] AttackInRangeAI
UNIT Lythos FalcoKnight 0x00 Level(3,Enemy,1) [12,19] 0x0 0x0 0x0 0x0 [IronLance] PursueWithoutHeedAI
UNIT Lythos DragonKnight 0x00 Level(3,Enemy,1) [15,19] 0x0 0x0 0x0 0x0 [Javelin] PursueWithoutHeedAI
UNIT

T5Group:
//UNIT PirateGeneric DracoZombie 0x0 Level(1,Enemy,1) [19,0] 0x0 0x0 0 0 [WretchedAir] PursueWithoutHeedAI //Ice Dragon, Dragon Zombie for now
UNIT Lythos Hero 0x0 Level(4,Enemy,1) [22,15] 0x0 0x0 1 LythosREDA1 [Wyrmslayer] AttackInRangeAI
UNIT Lythos Sage 0x0 Level(4,Enemy,1) [22,15] 0x0 0x0 1 LythosREDA2 [Elfire] AttackInRangeAI
UNIT Lythos FalcoKnight 0x00 Level(3,Enemy,1) [0,7] 0x0 0x0 0x0 0x0 [SlimLance] PursueWithoutHeedAI
UNIT Lythos DragonKnight 0x00 Level(3,Enemy,1) [0,10] 0x0 0x0 0x0 0x0 [SteelLance] PursueWithoutHeedAI
UNIT Lythos FalcoKnight 0x00 Level(3,Enemy,1) [0,13] 0x0 0x0 0x0 0x0 [IronLance] PursueWithoutHeedAI
UNIT Lythos DragonKnight 0x00 Level(3,Enemy,1) [0,16] 0x0 0x0 0x0 0x0 [ShortSpear] PursueWithoutHeedAI
UNIT

T6Group:
UNIT Lythos Hero 0x0 Level(4,Enemy,1) [22,15] 0x0 0x0 1 LythosREDA1 [SteelSword] AttackInRangeAI
UNIT Lythos Sage 0x0 Level(4,Enemy,1) [22,15] 0x0 0x0 1 LythosREDA2 [Thoron] AttackInRangeAI
UNIT PirateGeneric Archer 0x00 Level(15,Enemy,1) [4,10] 0x0 0x0 0x0 0x0 [SteelBow] PursueWithoutHeedAI
UNIT PirateGeneric Thief 0x00 Level(15,Enemy,1) [4,14] 0x0 0x0 0x0 0x0 [KillingEdge] PursueWithoutHeedAI
UNIT

OpeningMarthSheeda:
UNIT Marth EirikaMasterLord 0x00 Level(1,Ally,0) [14,11] 0x0  0x0 0x0 0x0 [SilverSword, Vulnerary] NoAI
UNIT Sheeda FalcoKnight 0x00 Level(1,Ally,0) [13,11] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT

EndingGood:
UNIT Marth EirikaMasterLord 0x00 Level(1,Ally,0) [14,11] 0x0  0x0 0x0 0x0 [SilverSword, Vulnerary] NoAI
UNIT Sheeda FalcoKnight 0x00 Level(1,Ally,0) [13,11] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT Chiki Mamkute_F 0x00 Level(1,Ally,0) [14,12] 0x0 0x0 0x0 0x0 [DivineStone] NoAI
UNIT

EndingDragons:
UNIT PirateGeneric DracoZombie 0x0 Level(1,Enemy,1) [15,3] 0x0 0x0 0 0 [WretchedAir] PursueWithoutHeedAI //Ice Dragon, Dragon Zombie for now
UNIT PirateGeneric DracoZombie 0x0 Level(1,Enemy,1) [16,4] 0x0 0x0 0 0 [WretchedAir] PursueWithoutHeedAI //Ice Dragon, Dragon Zombie for now
UNIT PirateGeneric DracoZombie 0x0 Level(1,Enemy,1) [17,2] 0x0 0x0 0 0 [WretchedAir] PursueWithoutHeedAI //Ice Dragon, Dragon Zombie for now
UNIT

Traps1:
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

BeginningScene:
LOAD2 0x1 OpeningMarthSheeda
CAM1 Marth
MUSC M_ThemeOfLove
Text(Ship,Ch6OpeningOnABoatAgain)
MUSC M_OffToWar
CAM1 [6,12]
STAL 10
TILECHANGE 4
STAL 30
Text(Ship,Ch6OpeningPiratesSpotted)
FADI 10
LOAD1 0x1 Good
LOAD1 0x1 Bad
LOAD1 0x1 GazackCloneSixGroup
CAM1 [0,0]
MUSC M_PreperationsFE11
FADU 10
STAL 10
CAM1 [29,0]
STAL 10
CAM1 [29,19]
STAL 10
CAM1 [0,19]
STAL 10
CAM1 [0,0]
STAL 10
GotoPrepScreen
ENDA

EndingScene:
WEATHER 2 //blizzard
MUSC M_Victory
CHECK_EVENTID 0x2
BNE 601 s0 sC
Text(Ship,Ch6LythosRetreatBoss)
GOTO 602
LABEL 601
Text(Ship,Ch6LythosRetreatNoBoss)
LABEL 602
FADI 10
CLEA 0
CLEN 0
CLEE 0
ENUN
LOAD2 0x1 EndingGood
FADU 10
TILEREVERT 2
STAL 10
TILEREVERT 1
STAL 10
TILEREVERT 3
TILEREVERT 4
Text(Ship,Ch6GettingWindy)
TILECHANGE 5
EVBIT_T 9
MOVE 10 Sheeda [24,5]
ENUN
STAL 10
MOVE 10 Sheeda [24,17]
ENUN
STAL 10
MOVE 10 Sheeda [3,17]
ENUN
STAL 10
MOVE 10 Sheeda [3,5]
ENUN
STAL 10
MOVE 12 Sheeda [13,11]
ENUN
STAL 10
EVBIT_F 9
MUSC M_EvilOnes
Text(Ship,Ch6LandNearby)
STAL 10
CAM1 [16,5]
TILECHANGE 6
STAL 30
SOUN 0xAF //snag destroyed
EARTHQUAKE 1 1
STAL 120
EARTHQUAKE_END
Text(Ship,Ch6HitRock)
SOUN 0xE6 //dragon roar
LOAD2 0x1 EndingDragons
ENUN
MOVE 10 Sheeda [16,6]
STAL 20
SOUN 0x37B //dragon roar
STAL 10
SOUN 0x11A //Bolting
STAL 10
MOVE 10 Sheeda [13,11]
ENUN
//SOUN 0x4E //wind noises
SOUN 0x2D9 //thud sound, probably something better to represent dropping the Divine Stone
STAL 20
EARTHQUAKE 1 1
TEXTSTART
TEXTSHOW Ch6Shipwrecked
TEXTEND
CAM1 Chiki
SOUN 0x11A //Bolting
TEXTCONT
TEXTEND
SOUN 0x11A //Bolting
TEXTCONT
TEXTEND
STAL 10
FAWI 20
SOUN 0x11A //Bolting
STAL 10
FAWU 20
MOVE 12 Chiki [0,10]
STAL 10
MOVE 12 Marth [12,11]
ENUN
CAM1 Marth
SOUN 0xBD //tile sinking into the water, ideally a splash sound
TEXTCONT
TEXTEND
FAWI 20
SOUN 0x11A //Bolting
STAL 10
FAWU 20
MOVE 12 Marth [13,6]
MOVE 6 Sheeda [13,9]
ENUN
REMU Marth
SOUN 0xBD //tile sinking into the water, ideally a splash sound
TEXTCONT
TEXTEND
CLEAN
STAL 10
FAWI 20
SOUN 0x11A //Bolting
STAL 10
FAWU 20
MOVE 5 Sheeda [1,0]
SOUN 0x11A //Bolting
STAL 40
SOUN 0xAF //snag destroyed
STAL 40
SOUN 0xE6 //dragon roar
STAL 40
ENUN
//FAWI 30
EARTHQUAKE_END
MNC2 0x7
ENDA

AfterPrepTorches:
CHECK_DEPLOYED Chiki
BNE 603 s0 sC
CAM1 Chiki
FlashCursor(Chiki,60)
GOTO 604
LABEL 603
CAM1 Marth
LABEL 604
Text(Ch6AfterPrepGetTorches)
SVAL s3 Torch
GIVEITEMTO Marth
NoFade
ENDA

PiratePlanksDrop:
TILECHANGE 3
NoFade
ENDA

LythosBoatArrives:
LOAD1 0x1 T3Group
FADI 10
MUSC M_Conspiracy
TILECHANGE 1
LOAD1 0x1 LythosBoatGroup
CAM1 Louis
FADU 10
STAL 10
Text(Ch6LythosFleetArrives)
NoFade
ENDA

LythosPlanksDrop:
TILECHANGE 2
NoFade
ENDA

Turn2Reinforcements:
LOAD1 0x1 T2Group
NoFade
ENDA

Turn4Reinforcements:
LOAD1 0x1 T4Group
NoFade
ENDA

Turn5Reinforcements:
LOAD1 0x1 T5Group
NoFade
ENDA

Turn6Reinforcements:
LOAD1 0x1 T6Group
NoFade
ENDA

LythosREDA1:
REDA [22,14]

LythosREDA2:
REDA [23,15]