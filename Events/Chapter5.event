EventPointerTable(0x17, PointerList)

ALIGN 4
PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Good Good  //This unit group determines the number and position of deployment slots when there is a prep screen
POIN $0 $0 $0
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0x0, AfterPrepScreen, 1)
TurnEventPlayer(0x0, LoadVillageWrecker,5)
END_MAIN

CharacterBasedEvents:
END_MAIN

LocationBasedEvents:
Village(0x7,Ch5WestVillage,4,10)
Village(0x8,Ch5EastVillage,28,5)
Village(0x9,Ch5RuinedVillage,22,9)
Armory(Ch5Armoury,26,2)
House(0x0,Ch5House1,6,12)
House(0x0,Ch5House2,21,3)
#ifdef __DEBUG__
Seize(0x3,EndingScene,5,4)
#endif
END_MAIN

MiscBasedEvents:
DefeatBoss(EndingScene)
CauseGameOverIfLordDies
END_MAIN

Dunno1:
END_MAIN

Dunno2:
END_MAIN

Dunno3:
END_MAIN

Tutorial:
END_MAIN

Good:
UNIT Marth EirikaMasterLord 0x00 Level(1,Ally,0) [5,4] 0x0  0x0 0x0 0x0 [Falchion] NoAI
UNIT Sheeda FalcoKnight 0x00 Level(1,Ally,0) [7,4] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT Chiki Mamkute_F 0x00 Level(1,Ally,0) [4,5] 0x0 0x0 0x0 0x0 [DivineStone] NoAI
UNIT Mario Berserker 0x00 Level(1,Ally,0) [5,6] 0x0 0x0 0x0 0x0 [Hammer] NoAI
UNIT Luigi Berserker 0x00 Level(1,Ally,0) [6,8] 0x0 0x0 0x0 0x0 [Hammer] NoAI
UNIT Luigi Berserker 0x00 Level(1,Ally,0) [7,7] 0x0 0x0 0x0 0x0 [Hammer] NoAI
UNIT Mario Berserker 0x00 Level(1,Ally,0) [2,4] 0x0 0x0 0x0 0x0 [Hammer] NoAI
UNIT Mario Berserker 0x00 Level(1,Ally,0) [3,2] 0x0 0x0 0x0 0x0 [Hammer] NoAI
UNIT Mario Berserker 0x00 Level(1,Ally,0) [5,7] 0x0 0x0 0x0 0x0 [Hammer] NoAI
UNIT Mario Berserker 0x00 Level(1,Ally,0) [2,7] 0x0 0x0 0x0 0x0 [Hammer] NoAI
UNIT Mario Berserker 0x00 Level(1,Ally,0) [4,6] 0x0 0x0 0x0 0x0 [Hammer] NoAI
UNIT Mario Berserker 0x00 Level(1,Ally,0) [3,3] 0x0 0x0 0x0 0x0 [Hammer] NoAI
UNIT

Bad:
UNIT Lythos ArmourKnight Clanne Level(5,Enemy,1) [9,10] 0x0 0x0 0x0 0x0 [Javelin] NeverMoveAI
UNIT Lythos Pirate Clanne Level(5,Enemy,1) [15,1] 0x0 0x0 0x0 0x0 [IronAxe] ChaseOnceApproachedAI
UNIT Lythos Pirate Clanne Level(5,Enemy,1) [11,13] 0x0 0x0 0x0 0x0 [IronAxe] ChaseOnceApproachedAI
UNIT Lythos Mercenary Clanne Level(5,Enemy,1) [6,11] 0x0 0x0 0x0 0x0 [IronSword] ChaseOnceApproachedAI
UNIT Lythos Thief Clanne Level(3,Enemy,1) [4,11] 0x0 0x0 0x0 0x0 [IronSword] BrigandAI
UNIT Lythos LanceKnight Clanne Level(5,Enemy,1) [21,4] 0x0 0x0 0x0 0x0 [IronLance] ChaseOnceApproachedAI
UNIT Lythos FreeKnight Clanne Level(5,Enemy,1) [21,5] 0x0 0x0 0x0 0x0 [IronSword] ChaseOnceApproachedAI
UNIT Lythos Fighter Clanne Level(5,Enemy,1) [15,6] 0x0 0x0 0x0 0x0 [IronAxe] AttackInRangeAI
UNIT Lythos Fighter Clanne Level(5,Enemy,1) [16,7] 0x0 0x0 0x0 0x0 [IronAxe] AttackInRangeAI
UNIT Lythos Archer Clanne Level(5,Enemy,1) [15,5] 0x0 0x0 0x0 0x0 [SteelBow] AttackInRangeAI
UNIT Lythos Mage Clanne Level(5,Enemy,1) [27,8] 0x0 0x0 0x0 0x0 [Thunder] AttackInRangeAI
UNIT Lythos Priest Clanne Level(5,Enemy,1) [26,10] 0x0 0x0 0x0 0x0 [Heal] HealUnits
UNIT Lythos Shaman Clanne Level(5,Enemy,1) [23,10] 0x0 0x0 0x0 0x0 [Flux] AttackInRangeAI
UNIT

Bad2:
UNIT Lythos Fleet Clanne Level(5,Enemy,1) [17,12] 0x0 0x0 0x0 0x0 [FireGun] PursueWithoutHeedAI
UNIT Lythos Fleet Clanne Level(5,Enemy,1) [11,1] 0x0 0x0 0x0 0x0 [FireGun] PursueWithoutHeedAI
UNIT Lythos ArmourKnight Clanne Level(5,Enemy,1) [25,3] 0x0 0x0 0x0 0x0 [Javelin] AttackInRangeAI
UNIT

BadJam:
UNIT Lythos General Alear2 Level(5,Enemy,1) [9,10] 0x0 0x0 0x0 0x0 [Spear] NeverMoveAI
UNIT Lythos Berserker Alear2 Level(5,Enemy,1) [15,1] 0x0 0x0 0x0 0x0 [SteelAxe] ChaseOnceApproachedAI
UNIT Lythos Berserker Alear2 Level(5,Enemy,1) [11,13] 0x0 0x0 0x0 0x0 [SteelAxe] ChaseOnceApproachedAI
UNIT Lythos Hero Alear2 Level(5,Enemy,1) [6,11] 0x0 0x0 0x0 0x0 [SteelSword] ChaseOnceApproachedAI
UNIT Lythos Thief Alear2 Level(20,Enemy,1) [4,11] 0x0 0x0 0x0 0x0 [KillingEdge] BrigandAI
UNIT Lythos Paladin Alear2 Level(5,Enemy,1) [21,4] 0x0 0x0 0x0 0x0 [SteelLance] ChaseOnceApproachedAI
UNIT Lythos Paladin Alear2 Level(5,Enemy,1) [21,5] 0x0 0x0 0x0 0x0 [SteelSword] ChaseOnceApproachedAI
UNIT Lythos Warrior Alear2 Level(5,Enemy,1) [15,6] 0x0 0x0 0x0 0x0 [SteelAxe] AttackInRangeAI
UNIT Lythos Warrior Alear2 Level(5,Enemy,1) [16,7] 0x0 0x0 0x0 0x0 [SteelAxe] AttackInRangeAI
UNIT Lythos Sniper Alear2 Level(5,Enemy,1) [15,5] 0x0 0x0 0x0 0x0 [SilverBow] AttackInRangeAI
UNIT Lythos Sage Alear2 Level(5,Enemy,1) [27,8] 0x0 0x0 0x0 0x0 [Thunder] AttackInRangeAI
UNIT Lythos Bishop Alear2 Level(5,Enemy,1) [26,10] 0x0 0x0 0x0 0x0 [Divine, Mend] HealUnits
UNIT Lythos Druid Alear2 Level(5,Enemy,1) [23,10] 0x0 0x0 0x0 0x0 [Luna] AttackInRangeAI
UNIT

Bad2Jam:
UNIT Lythos Fleet Alear2 Level(10,Enemy,1) [17,12] 0x0 0x0 0x0 0x0 [FireGun] PursueWithoutHeedAI
UNIT Lythos Fleet Alear2 Level(10,Enemy,1) [11,1] 0x0 0x0 0x0 0x0 [FireGun] PursueWithoutHeedAI
UNIT Lythos General Alear2 Level(5,Enemy,1) [25,3] 0x0 0x0 0x0 0x0 [Javelin] AttackInRangeAI
UNIT

ClanneGroup:
UNIT Clanne Mage 0x00 Level(10,Enemy,0) [25,7] 0x0 0x0 0x0 0x0 [Elfire] NeverMoveAI //replace with Clanne or whoever
UNIT

DivineOne:
UNIT Alear2 Swordmaster_F /*DracoZombie*/ 0x00 Level(20,Enemy,0) [25,7] 0x0 0x0 0x0 0x0 [WretchedAir] NeverMoveAI
UNIT

CutsceneAlear:
UNIT Alear Swordmaster_F 0x00 Level(10,Enemy,0) [12,1] 0x0 0x0 0x0 0x0 [SilverSword] NeverMoveAI
UNIT 0x80 /*Vander*/ KnightPaladin 0x00 Level(10,Enemy,0) [12,2] 0x0 0x0 0x0 [SilverLance] NeverMoveAI
UNIT

CutsceneMarthOutside:
UNIT Marth StarLord 0x00 Level(1,Ally,0) [2,4] 0x0  0x0 0x0 0x0 [Falchion] NoAI
UNIT Sheeda FalcoKnight 0x00 Level(1,Ally,0) [3,5] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT

CutsceneMarthInside:
UNIT Marth StarLord 0x00 Level(1,Ally,0) [6,10] 0x0  0x0 0x0 0x0 [Falchion] NoAI
UNIT Sheeda KnightFalcon 0x00 Level(1,Ally,0) [8,10] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT

VillageWrecker:
UNIT Lythos Thief 0x00 Level(20,Enemy,1) [16,4] 0x0 0x0 0x0 0x0 [IronSword] BrigandAI
UNIT

EndingGood:
UNIT Marth StarLord 0x00 Level(1,Ally,0) [25,7] 0x0  0x0 0x0 0x0 [Falchion] NoAI
UNIT Sheeda FalcoKnight 0x00 Level(1,Ally,0) [26,7] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT Chiki Mamkute_F 0x00 Level(1,Ally,0) [25,6] 0x0 0x0 0x0 0x0 [DivineStone] NoAI
UNIT

Traps1:
SavePoint(20,4)
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

BeginningScene:
LOMA 0x42
LOAD1 0x1 CutsceneAlear
MUSC M_Conspiracy
CAM1 Alear
FADU 10
FlashCursor(Alear,60)
Text(Castle_Hall,Ch5ThatMeddlingSheeda)
FADI 10
CLEE 0
LOMA 0x4
LOAD2 0x1 CutsceneMarthOutside
CAM1 Marth
FADU 10
FlashCursor(Sheeda,60)
Text(Harbor,Ch5OffTheBoat)
MOVE 8 Marth [5,4]
MOVE 8 Sheeda [5,4]
ENUN
CLEA 0
FADI 10
CALL DismountSheeda
LOMA 0x42
LOAD1 0x1 CutsceneAlear
LOAD2 0x1 CutsceneMarthInside
TILECHANGE 5 //open the door by Alear
CAM1 Alear
MUSC M_OffToWar
FADU 10
STAL 10
MOVE 10 Marth [11,3]
MOVE 10 Sheeda [13,3]
ENUN
STAL 10
Text(Castle_Hall,Ch5ConfrontingAlear)
MOVE 10 Marth [11,1]
ENUN
StartBattle
CriticalHit(0,27)
NormalDamage(1,5)
EndAttack
FIGHT Marth Alear Falchion 0
MUSC M_ThemeOfLove
Text(Castle_Hall,Ch5AfterScriptedBattle)
FADI 10
CALL MountSheeda
CLEA 0
CLEE 0
LOMA 0x4
LOAD3 0x0 Good
CHECK_EVENTID JamModeOn
BNE 501 sC s0 //branch if jam mode 
LOAD1 0x1 ClanneGroup
LOAD1 0x1 Bad
LOAD1 0x1 Bad2
GOTO 502
LABEL 501
//the pre-release script assumed Alear was truthful... but I know the truth
//dragon zombie she will be!
LOAD1 0x1 DivineOne
LOAD1 0x1 BadJam
LOAD1 0x1 Bad2Jam
LABEL 502
MUSC M_PreperationsFE11
CAM1 [0,0]
FADU 10
STAL 10
CAM1 [31,0]
STAL 10
CAM1 [31,13]
STAL 10
CAM1 [0,13]
STAL 10
CAM1 [0,0]
GotoPrepScreen
ENDA

EndingScene:
FADI 10
CLEA 0
CLEN 0
CLEE 0
LOAD2 0x1 EndingGood
CAM1 Marth
FADU 10
CHECK_EVENTID JamModeOn
BEQ 503 sC s0 //branch if not jam mode 
MUSC M_Victory
TEXTSTART
TEXTSHOW Ch5JamModeEnding
TEXTEND
MUSC M_Conspiracy
TEXTCONT
TEXTEND
//oh no, contract!
TurnEnemy(Marth)
TurnEnemy(Sheeda)
TurnEnemy(Chiki)
STAL 30
TEXTCONT
TEXTEND
//oh yes, it's void!
TurnAlly(Sheeda)
STAL 30
TEXTCONT
TEXTEND
TurnAlly(Marth)
TurnAlly(Chiki)
STAL 30
TEXTCONT
TEXTEND
MUSC M_LegendOfTheDivineDragonMusicBox
TEXTCONT
TEXTEND
STAL 10
FADI 30
CLEA 0
CLEE 0
CLEN 0
REMA
ASMC 0x30185 //clears map tilesets?
EVBIT_F 0x2
STAL 120
MUSC M_EndingMedleyBook1
ASMC 0xB8175 //turn counts display
STAL 1
CALL 0xA0036C //Character Endings
STAL 90
FADU 10
TUTORIALTEXTBOXSTART
CenterTutorialTextBox
TEXTSHOW EndOfJamModeMessage
TEXTEND
CALL ReturnToTitle

LABEL 503

//main game
MUSC M_TogetherWeRide
CALL UnitGachaAct1
WARPIN [25,8]
MOVE 0xFFFF 0xFFFD [25,8]
ENUN
ENDWARP
STAL 20

MUSC M_Victory
Text(Ch5BossDead)
MUSC M_ThemeOfLove
Text(Woodland_Night,Ch5MostynGrave)
MUSC M_Conspiracy
Text(Castle_Hall_Night,Ch5AlfonseAppears)
MUSC M_OffToWar
Text(Castle_Hall_Night,Ch5RodyEscapes)
Text(Castle_Hall_Night,Ch5MarthSailsNorth)
REMU Rody
SVAL s3 ShieldOfSeals
GIVEITEMTO Marth
WM_LOADLOCATION2 0 5 0 //Required to fix chapter name
WM_SETDESTINATION 0 5 0 //World map node. Keep these equal.
WM_SETNEXTLOCATION 0 0 5 //Needed for world map narration.
MNCH 0x6
ENDA

AfterPrepScreen:
MUSC M_OffToWar
CHECK_EVENTID JamModeOn
BNE 504 sC s0 //branch if jam mode
CAM1 Clanne
FlashCursor(Clanne,60)
Text(Ch5BossTaunt)
GOTO 505
LABEL 504
CAM1 Alear2
FlashCursor(Alear2,60)
TEXTSTART
TEXTSHOW Ch5AlearTauntJam
TEXTEND
FADI 16
//REMOVEPORTRAITS
//PROM Alear2 DracoZombie 0x00 //She's transforming pre-chapter
//REMOVEPORTRAITS
RECLASS Alear2 DracoZombie
FADU 16
STAL 30
TEXTCONT
TEXTEND
REMA
LABEL 505
NoFade
ENDA

Ch5WestVillage:
Text(Village,Ch5RiffVillage)
SVAL s3 Dragonshield
GIVEITEMTO 0xFFFF
NoFade
ENDA

Ch5EastVillage:
Text(Village,Ch5MoneyVillage)
CALL UnitGachaJam
WARPIN [28,5]
MOVE 0xFFFF 0xFFFD [28,5]
ENUN
ENDWARP
STAL 20
NoFade
ENDA

Ch5RuinedVillage:
Text(Village,Ch5RuinedVillageText)
SVAL s3 SilverLance
GIVEITEMTO 0xFFFF
NoFade
ENDA

Ch5House1:
Text(HouseBG,Ch5House1Text)
NoFade
ENDA

Ch5House2:
Text(HouseBG,Ch5House3Text)
NoFade
ENDA

LoadVillageWrecker:
CAM1 [16,4]
LOAD1 0x1 VillageWrecker
ENUN
NoFade
ENDA

Ch5Armoury:
SHLI IronSword IronBow KillerBow IronAxe IronLance Javelin
ALIGN 4

ReturnToTitle:
MNTS 0x0
ENDB