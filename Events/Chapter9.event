EventPointerTable(0x2A, PointerList)

ALIGN 4
PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Good Good  //This unit group determines the number and position of deployment slots when there is a prep screen
POIN $0 $0 $0
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0x0,AfterBattlePrep,1)
TurnEventNPC(0x0,GuinevereSendsReinforcements,2)
END_MAIN

CharacterBasedEvents:
CharacterEventBothWays(0x8,ReturnOfMiledy,Eliwood,Miledy)
END_MAIN

LocationBasedEvents:
Seize(0x3,EndingScene,10,2)
Village(0x7,StartingVillage,1,23)
House(0x0,FE7StartHouse1,17,16)
House(0x0,FE7StartHouse2,18,16)
Armory(Ch9Armoury,19,4)
Vendor(Ch9Vendor,20,4)
#ifdef __DEBUG__
Seize(0x3,EndingScene,11,25)
#endif
END_MAIN

MiscBasedEvents:
CauseGameOverIfLordDies
AREA 0xA BossCharges [0,0] [6,14] //approaching middle forts
AREA 0xA BossCharges [7,8] [13,13] //don't try fly-skipping
END_MAIN

Dunno1:
END_MAIN

Dunno2:
END_MAIN

Dunno3:
END_MAIN

Tutorial:
END_MAIN

Good:
UNIT Marth StarLord 0x00 Level(1,Ally,0) [11,25] 0x0  0x0 0x0 0x0 [SilverSword, Vulnerary] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [13,26] 0x0 0x0 0x0 0x0 [SilverLance, Vulnerary] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [15,24] 0x0 0x0 0x0 0x0 [SilverLance, Vulnerary] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [16,23] 0x0 0x0 0x0 0x0 [SilverLance, Vulnerary] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [13,23] 0x0 0x0 0x0 0x0 [SilverLance, Vulnerary] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [8,25] 0x0 0x0 0x0 0x0 [SilverLance, Vulnerary] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [6,26] 0x0 0x0 0x0 0x0 [SilverLance, Vulnerary] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [4,25] 0x0 0x0 0x0 0x0 [SilverLance, Vulnerary] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [3,24] 0x0 0x0 0x0 0x0 [SilverLance, Vulnerary] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [1,24] 0x0 0x0 0x0 0x0 [SilverLance, Vulnerary] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [9,26] 0x0 0x0 0x0 0x0 [SilverLance, Vulnerary] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [15,26] 0x0 0x0 0x0 0x0 [SilverLance, Vulnerary] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [3,26] 0x0 0x0 0x0 0x0 [SilverLance, Vulnerary] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [12,25] 0x0 0x0 0x0 0x0 [SilverLance, Vulnerary] NoAI
UNIT

Bad:
UNIT RogueBernBoss DragonMaster 0x00 Level(1,Enemy,0) [3,4] 0x0 0x0 0x0 0x0 [SilverLance] GuardTileAI
UNIT RogueBern DragonKnight RogueBernBoss Level(7,Enemy,1) [2,3] 0x0 0x0 0x0 [SteelLance] GuardTileAI
UNIT RogueBern DragonKnight RogueBernBoss Level(7,Enemy,1) [4,5] 0x0 0x0 0x0 [SteelLance] GuardTileAI
UNIT RogueBern DragonKnight RogueBernBoss Level(7,Enemy,1) [4,3] 0x0 0x0 0x0 [SteelLance] GuardTileAI
UNIT RogueBern DragonKnight RogueBernBoss Level(7,Enemy,1) [2,5] 0x0 0x0 0x0 [SteelLance] GuardTileAI
UNIT RogueBern Fighter RogueBernBoss Level(7,Enemy,1) [19,14] 0x0 0x0 0x0 [IronAxe] PursueWithoutHeedAI
UNIT RogueBern Fighter RogueBernBoss Level(7,Enemy,1) [16,16] 0x0 0x0 0x0 [Halberd] PursueWithoutHeedAI
UNIT RogueBern Brigand RogueBernBoss Level(7,Enemy,1) [20,17] 0x0 0x0 0x0 [SteelAxe] PursueWithoutHeedAI
UNIT RogueBern Archer RogueBernBoss Level(7,Enemy,1) [19,21] 0x0 0x0 0x0 [SteelBow] PursueWithoutHeedAI
UNIT RogueBern ArmourKnight RogueBernBoss Level(7,Enemy,1) [12,19] 0x0 0x0 0x0 [Javelin] GuardTileAI
UNIT RogueBern Archer RogueBernBoss Level(7,Enemy,1) [12,16] 0x0 0x0 0x0 [SteelBow] PursueWithoutHeedAI
UNIT RogueBern Soldier RogueBernBoss Level(7,Enemy,1) [9,17] 0x0 0x0 0x0 [IronLance] PursueWithoutHeedAI
UNIT RogueBern Shaman RogueBernBoss Level(7,Enemy,1) [8,19] 0x0 0x0 0x0 [Flux] PursueWithoutHeedAI
UNIT RogueBern AxeArmour RogueBernBoss Level(8,Enemy,1) [4,17] 0x0 0x0 0x0 [HandAxe] NeverMoveAI
UNIT RogueBern ArmourKnight RogueBernBoss Level(8,Enemy,1) [5,16] 0x0 0x0 0x0 [Javelin] NeverMoveAI
UNIT RogueBern Archer RogueBernBoss Level(8,Enemy,1) [5,15] 0x0 0x0 0x0 [Longbow] NeverMoveAI
UNIT

Bad2:
UNIT RogueBern DragonKnight RogueBernBoss Level(7,Enemy,1) [13,13] 0x0 0x0 0x0 [SteelLance] AttackInRangeAI
UNIT RogueBern Mercenary RogueBernBoss Level(10,Enemy,1) [0,1] DropItem 0x0 0x0 [IronBlade, GoddessIcon] AttackInRangeAI
UNIT RogueBern DragonKnight RogueBernBoss Level(7,Enemy,1) [4,5] 0x0 0x0 0x0 [SteelLance] AttackInRangeAI
UNIT RogueBern FreeKnight RogueBernBoss Level(7,Enemy,1) [9,7] 0x0 0x0 0x0 [SteelSword] AttackInRangeAI
UNIT RogueBern AxeKnight RogueBernBoss Level(7,Enemy,1) [11,7] 0x0 0x0 0x0 [SteelAxe] AttackInRangeAI
UNIT RogueBern Myrmidon RogueBernBoss Level(7,Enemy,1) [7,10] 0x0 0x0 0x0 [IronSword] AttackInRangeAI
UNIT RogueBern Shaman RogueBernBoss Level(7,Enemy,1) [8,11] 0x0 0x0 0x0 [Flux] AttackInRangeAI
UNIT RogueBern ArchKnight RogueBernBoss Level(7,Enemy,1) [20,7] 0x0 0x0 0x0 [SteelBow, Longbow] AttackInRangeAI
UNIT RogueBern ArchKnight RogueBernBoss Level(7,Enemy,1) [19,6] 0x0 0x0 0x0 [ShortBow] AttackInRangeAI
UNIT RogueBern BowArmour RogueBernBoss Level(10,Enemy,1) [15,10] 0x0 0x0 0x0 [SteelBow] NeverMoveAI
UNIT RogueBern Shaman RogueBernBoss Level(7,Enemy,1) [16,10] 0x0 0x0 0x0 [Luna] NeverMoveAI
UNIT

MiledySquad:
UNIT Miledy DragonKnight_F 0x0 Level(15,NPC,0) [10,2] 0x0 0x1 MiledyREDA [KillerLance, Elixir] GuardTileAI //no more pretending
UNIT Bern DragonKnight 0x0 Level(10,NPC,1) [10,2] 0x0 0x1 BernGeneric1REDA [SteelLance] AttackInRangeAI
UNIT Bern DragonKnight 0x0 Level(10,NPC,1) [10,2] 0x0 0x1 BernGeneric2REDA [SteelLance] AttackInRangeAI
UNIT Bern Mercenary 0x0 Level(10,NPC,1) [10,2] 0x0 0x1 BernGeneric3REDA [SteelBlade] AttackInRangeAI
UNIT

CutsceneOstiaCastle:
UNIT Lilina Mage_F 0x00 Level(10,NPC,0) [6,17] 0x0 0x0 0x0 0x0 [Fire] NoAI
UNIT Barth General 0x00 Level(5,NPC,0) [5,17] 0x0 0x0 0x0 0x0 [SilverLance] NoAI
UNIT Marth Lord 0x00 Level(1,Ally,0) [11,23] 0x0 0x0 0x0 0x0 [Rapier] NoAI
UNIT Eliwood Paladin 0x00 Level(1,Ally,0) [12,23] 0x0 0x0 0x0 0x0 [SilverSword] NoAI
UNIT

CutscenePheraeCastle:
UNIT Lilina Mage_F 0x00 Level(10,NPC,0) [9,7] 0x0 0x0 0x0 0x0 [Fire] NoAI
UNIT Eliwood Paladin 0x00 Level(1,Ally,0) [8,7] 0x0 0x0 0x0 0x0 [SilverSword] NoAI
UNIT Merlinus Civilian_2 0x00 Level(1,NPC,0) [8,1] 0x0 0x0 0x0 0x0 [Vulnerary] NoAI
UNIT Wolt Archer 0x00 Level(1,NPC,0) [4,8] 0x0 0x0 0x0 0x0 [IronBow] NoAI //cameos!
UNIT Marcus KnightPaladin 0x00 Level(1,NPC,0) [14,6] 0x0 0x0 0x0 0x0 [SilverLance] NoAI
UNIT Ostia KnightPaladin 0x00 Level(1,NPC,0) [7,7] 0x0 0x0 0x0 0x0 [IronSword] NoAI //Alan
UNIT Ostia KnightPaladin 0x00 Level(1,NPC,0) [10,7] 0x0 0x0 0x0 0x0 [IronLance] NoAI //Lance
UNIT

CutsceneOpeningBern:
UNIT Lilina Mage_F 0x00 Level(10,NPC,0) [13,24] 0x0 0x0 0x0 0x0 [Fire] NoAI
UNIT Eliwood Paladin 0x00 Level(1,Ally,0) [13,25] 0x0 0x0 0x0 0x0 [SilverSword] NoAI
UNIT Marth Lord 0x00 Level(1,Ally,0) [11,25] 0x0 0x0 0x0 0x0 [Rapier] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [10,25] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT Chiki Mamkute_F 0x00 Level(1,Ally,0) [11,26] 0x0 0x0 0x0 0x0 [Vulnerary] NoAI
UNIT RogueBernBoss DragonMaster 0x00 Level(1,Enemy,0) [1,1] 0x0 0x0 0x0 0x0 [SilverLance] AttackInRangeAI
UNIT Diamant MasterLord 0x00 Level(3,Enemy,0) [0,1] DropItem 0x0 0x0 0x0 [SilverSword/*, RoyRing, EarthOrb*/] GuardTileAI
UNIT RogueBern DragonKnight 0x00 Level(1,Enemy,1) [10,22] 0x0 0x0 0x0 0x0 [IronLance] NoAI
UNIT

LilinaGroup:
UNIT Lilina Mage_F 0x00 Level(10,Ally,0) [13,24] 0x0 0x0 0x0 0x0 [Elfire, Thunder, Vulnerary] NoAI
UNIT

CutsceneSomniel:
UNIT Alear DivineOne 0x00 Level(1,Enemy,0) [7,4] 0x0  0x0 0x0 0x0 [Falchion] NoAI
UNIT Amber KnightPaladin 0x00 Level(1,Enemy,0) [6,6] 0x0  0x0 0x0 0x0 [SilverLance] NoAI //really Alfred, who dies here
UNIT Celine Priestess 0x00 Level(1,Enemy,0) [8,6] 0x0 0x0 0x0 0x0 [Bolganone] NoAI
UNIT Vander KnightPaladin 0x00 Level(1,Enemy,0) [2,3] 0x0  0x0 0x0 0x0 [SilverLance] NoAI 
UNIT Diamant MasterLord 0x00 Level(1,Enemy,0) [0,6] 0x0  0x0 0x0 0x0 [MiracleSword] NoAI 
UNIT

Traps1:
SavePoint(9,16)
SavePoint(10,8)
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

BeginningScene:
LOMA 0x43 //Ostia Castle
LOAD2 0x1 CutsceneOstiaCastle
MUSC M_WhenTheRushComes
CAM1 Lilina
FADU 10
//Text box: Ostia Castle
FlashCursor(Lilina,60)
Text(Castle_Room,Ch9OpeningLilinaBarth)
RECLASS Eliwood KnightPaladin //indoor cutscene!
STAL 5
MOVE 10 Barth [11,21]
ENUN
MOVE 10 Marth [7,19]
MOVE 10 Eliwood [6,19]
ENUN
STAL 10
FlashCursor(Lilina,60)
Text(Castle_Room,Ch9OpeningLilinaMeeting)
Text(Castle_Room,Ch9LilinaPlotDump)
//this text is too long; clip out a bit of the emblem plot dump?
MOVE 10 Lilina [10,21]
ENUN
MOVE 10 Barth [6,17]
ENUN
MOVE 10 Lilina [18,19]
MOVE 10 Eliwood [18,22]
MOVE 10 Marth [18,23]
ENUN
FADI 10
CLEA 0
CLEN 0
LOMA 10 //Pherae
LOAD2 0x1 CutscenePheraeCastle
FADU 10
//Text box: Pherae Castle
FlashCursor(Eliwood,60)
Text(Ch9OpeningPheraeIntro)
STAL 10
MOVE 10 Merlinus [8,5]
ENUN
SOUN 0xB1 //Door opening
TILECHANGE 5 //throne room door
STAL 60
MOVE 10 Merlinus [8,1]
MOVE 10 Eliwood [8,3]
MOVE 10 Lilina [9,3]
ENUN
STAL 10
FlashCursor(Merlinus,60)
Text(Castle_Hall,Ch9OpeningPherae)
MOVE 10 Eliwood [4,13]
MOVE 10 Lilina [5,13]
ENUN
FADI 10
CLEA 0
CLEN 0
LOMA 9 //actual chapter
STAL 5
LOAD2 0x1 CutsceneOpeningBern
RECLASS Eliwood Paladin
CAM1 Marth
FADU 10
//location box
FlashCursor(Eliwood,60)
Text(Woodland,Ch9OpeningApproachingBernKeep)
EVBIT_T 9 //follow
MOVE 10 Sheeda [7,20]
ENUN
MOVE 12 RogueBern [1,2]
ENUN
MUSC M_Stratagem
STAL 10
FlashCursor(RogueBernBoss,60)
Text(Woodland,Ch9OpeningRebellion)
FADI 10
CLEA 0
CLEE 0
CLEN 0
LOAD3 0x1 Good
LOAD1 0x1 Bad
LOAD1 0x1 Bad2
CAM1 [0,0]
MUSC M_EnemiesAppear
FADU 10
STAL 20
CAM1 [20,0]
STAL 20
CAM1 [20,26]
STAL 20
CAM1 [0,26]
STAL 20
CAM1 [0,0]
STAL 10
GotoPrepScreen
ENDA

EndingScene:
CHECK_EVENTID NoSummonGun
BNE 901 s0 sC
CHECK_EVENTID 0x8 //Miledy
BNE 901 s0 sC
MUSC M_JoinUs
CALL UnitGachaAct2
WARPIN [8,3]
MOVE 0xFFFF 0xFFFD [8,3]
ENUN
ENDWARP
LABEL 901
MUSC M_Triumph
Text(Castle_Hall,Ch9EndingBernKeep)
Text(Castle_Hall,Ch9EndingBernKeep2)
CHECK_EVENTID 0x8 //Miledy
BEQ 902 s0 sC //skip if not recruited
Text(Castle_Hall,Ch9EndingMiledyOrders)

FADI 10
CLEA 0
CLEE 0
CLEN 0
LOMA 26 //Somniel
LOAD2 0x1 CutsceneSomniel
MUSC M_Stratagem
FADU 10
BROWNBOX LocationSomniel [0,0]
FlashCursor(Amber,60)
Text(Castle_Room,Ch9EndingAlfredDies)
KILL Amber
AWAIT_KILL Amber //Alfred
STAL 120
MOVE 10 Vander [5,6]
MOVE 10 Diamant [4,6]
ENUN
FlashCursor(Celine,60)
Text(Castle_Room,Ch9EndingCelineMourns)
WM_LOADLOCATION2 0 8 0 //Required to fix chapter name
WM_SETDESTINATION 0 8 0 //World map node. Keep these equal.
WM_SETNEXTLOCATION 0 0 8 //Needed for world map narration.
MNCH 0xA
ENDA


AfterBattlePrep:
//Written after prep would flow better as a T2 or so
//Just a quick line along the lines of "don't worry, I can defend myself"
LOAD1 0x1 LilinaGroup
CAM1 Lilina
FlashCursor(Lilina,60)
//Text(PlaceholderText)
NoFade
ENDA

GuinevereSendsReinforcements:
//TODO: Figure out how to stop Miledy and friends getting immediately attacked
CAM1 [10,2]
STAL 10
FlashCursor(10,2,60)
Text(Castle_Hall,Ch9GuinevereReaction)
LOAD1 0x1 MiledySquad
NoFade
ENDA

StartingVillage:
Text(Outside_Village_Day,Ch9Village)
SVAL s3 Longbow
GIVEITEMTO 0xFFFF
NoFade
ENDA

ReturnOfMiledy:
//The more smooth way would be to talk to her, then Guin lets her go in chapter ending
MUSC M_JoinUs
Text(Ch9MiledyChat)
TurnAlly(Miledy)
NoFade
ENDA

BossCharges:
//Swap from Guard to Pursue once approached: stop Miledy from getting killed
SVAL s1 0 //Is active blue?
CHECK_ALLEGIANCE 0xFFFF //active
BNE 905 s0 sC
SVAL s1 0x00000000 //pursue
CHAI RogueBernBoss
CHAI [2,3] //the dragon knights immediately around him
CHAI [4,5]
CHAI [4,3]
CHAI [2,5]
GOTO 906
LABEL 905
ENUF 0xA //unset flag
LABEL 906
NoFade
ENDA

Ch9Armoury:
SHLI IronSword IronBlade IronLance IronAxe IronBow Javelin HandAxe
ALIGN 4

Ch9Vendor:
SHLI Fire Thunder Wind Elwind Flux Heal Mend Barrier Vulnerary DoorKey
ALIGN 4

FE7StartHouse1:
Text(HouseBG,Ch9House1)
NoFade
ENDA

FE7StartHouse2:
Text(HouseBG,Ch9House2)
NoFade
ENDA

MiledyREDA:
REDA [10,3]

BernGeneric1REDA:
REDA [9,4]

BernGeneric2REDA:
REDA [11,4]

BernGeneric3REDA:
REDA [10,5]