EventPointerTable(0x4B, PointerList)

ALIGN 4
PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Good Good  //This unit group determines the number and position of deployment slots when there is a prep screen
POIN $0 $0 $0
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0x0,AfterPrep,1)
TurnEventEnemy(0x0,InitialWarpRagnarok,2)
END_MAIN

CharacterBasedEvents:
CharacterEventBothWays(0x7,GreyStandsDown,Claire,Grey)
CharacterEventBothWays(0x8,MycenMoves,Grey,Mycen)
//Mycen calms Alm down, variants for any other Valencian
//Louis makes Celine think long and hard...
//and Marth makes her surrender
//Or Alm kills her!
//There's probably a better way to go about this. Turn Grey blue as a temporary unit?
//I've done the same with Barth, so there's precedent.
END_MAIN

LocationBasedEvents:
#ifdef __DEBUG__
Seize(11,31)
#endif
Door(4,28)
Door(18,28)
Chest(IronSword,2,31)
Chest(IronBlade,5,30)
Chest(IronBlade,7,32)
Chest(IronBlade,15,32)
Chest(IronBlade,16,29)
Chest(IronBlade,20,30)
Chest(IronBlade,19,2)
END_MAIN

MiscBasedEvents:
//Let's pull a Zephiel for entering the throne room.
//Hit switches directly under the stairs (or kill Mycen), the throne room (or seize it), and by the Doma rep.
AREA 0x10 LeftSwitchHit [3,18] [3,18]
AREA 0x11 MiddleSwitchHit [10,18] [12,18]
AREA 0x12 RightSwitchHit [20,0] [20,0]
CauseGameOverIfLordDies
END_MAIN

Dunno1:
END_MAIN

Dunno2:
END_MAIN

Dunno3:
END_MAIN

Tutorial:
END_MAIN

Good:
UNIT Marth Lord 0x00 Level(1,Ally,0) [11,31] 0x0  0x0 0x0 0x0 [SilverSword, Vulnerary] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [9,31] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [10,31] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [12,29] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [11,32] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [10,33] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [9,32] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [11,34] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [13,34] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [10,35] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [10,30] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [11,29] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT

Bad:
//Chapter bosses
UNIT Alm HeroAlm 0x00 Level(5,Enemy,0) [9,3] 0x0 0x0 0x0 0x0 [Kingsfang, LifeOrb] ChaseOnceApproachedAI
UNIT Celine Priestess 0x00 Level(5,Enemy,0) [10,3] DropItem 0x0 0x0 0x0 [Bolganone/*Ragnarok*/, ShadowKiller, CelicaRing] PursueWithoutHeedAI
UNIT Grey Hero Alm Level(3,Enemy,0) [11,20] DropItem 0x0 0x0 0x0 [ThunderSword, SilverAxe] ChaseOnceApproachedAI
UNIT Mycen KnightPaladin Alm Level(3,Enemy,0) [3,18] DropItem 0x0 0x0 0x0 [SilverSword, Elixir] GuardTileAI
UNIT Valencia General Alm Level(3,Enemy,1) [5,10] 0x0 0x0 0x0 0x0 [Spear] NeverMoveAI
UNIT Valencia General Alm Level(3,Enemy,1) [4,10] 0x0 0x0 0x0 0x0 [Spear] NeverMoveAI
UNIT DomaFaithfulBoss Summoner 0x00 Level(3,Enemy,0) [20,5] DropItem 0x0 0x0 0x0 [Fenrir, Berserk, GuidingRing] AttackInRangeAI
//Starting group, by Grey
UNIT Valencia Soldier Alm Level(16,Enemy,1) [10,22] 0x0 0x0 0x0 0x0 [IronLance] PursueWithoutHeedAI
UNIT Valencia Soldier Alm Level(16,Enemy,1) [12,22] 0x0 0x0 0x0 0x0 [IronLance] PursueWithoutHeedAI
UNIT Valencia Mercenary Alm Level(16,Enemy,1) [11,23] 0x0 0x0 0x0 0x0 [IronBlade] PursueWithoutHeedAI
UNIT Valencia Mercenary Alm Level(16,Enemy,1) [19,22] 0x0 0x0 0x0 0x0 [SteelSword] PursueWithoutHeedAI
UNIT Valencia Mage Alm Level(16,Enemy,1) [16,25] 0x0 0x0 0x0 0x0 [Elwind] PursueWithoutHeedAI
UNIT Valencia Archer Alm Level(16,Enemy,1) [7,25] 0x0 0x0 0x0 0x0 [SteelBow] PursueWithoutHeedAI
UNIT Valencia Mage Alm Level(16,Enemy,1) [2,23] 0x0 0x0 0x0 0x0 [Thunder] PursueWithoutHeedAI
UNIT Valencia Soldier Alm Level(16,Enemy,1) [3,26] 0x0 0x0 0x0 0x0 [IronLance] PursueWithoutHeedAI
UNIT

BadDoma:
//Corridor to the right, full of monsters
UNIT DomaFaithful Shaman DomaFaithfulBoss Level(16,Enemy,1) [20,17] 0x0 0x0 0x0 0x0 [Flux] AttackInRangeAI
UNIT DomaFaithful Shaman DomaFaithfulBoss Level(16,Enemy,1) [21,18] 0x0 0x0 0x0 0x0 [Flux] AttackInRangeAI
UNIT DomaFaithful Bonewalker DomaFaithfulBoss Level(16,Enemy,1) [21,12] 0x0 0x0 0x0 0x0 [SteelSword] AttackInRangeAI
UNIT DomaFaithful Bonewalker DomaFaithfulBoss Level(16,Enemy,1) [20,12] 0x0 0x0 0x0 0x0 [SteelSword] AttackInRangeAI
UNIT DomaFaithful Bonewalker DomaFaithfulBoss Level(16,Enemy,1) [19,8] 0x0 0x0 0x0 0x0 [Armourslayer] AttackInRangeAI
UNIT DomaFaithful DracoZombie DomaFaithfulBoss Level(1,Enemy,1) [20,1] 0x0 0x0 0x0 0x0 [WretchedAir] GuardTileAI
UNIT DomaFaithful Revenant DomaFaithfulBoss Level(16,Enemy,1) [21,9] 0x0 0x0 0x0 0x0 [RottenClaw] AttackInRangeAI
UNIT DomaFaithful Entombed DomaFaithfulBoss Level(1,Enemy,1) [19,2] 0x0 0x0 0x0 0x0 [SharpClaw] AttackInRangeAI
UNIT

BadTreasureRooms:
//Left room
UNIT Firene Halberdier Celine Level(1,Enemy,1) [4,29] 0x0 0x0 0x0 0x0 [SteelLance] PursueWithoutHeedAI
UNIT Firene Archer Celine Level(16,Enemy,1) [4,32] 0x0 0x0 0x0 0x0 [Longbow] PursueWithoutHeedAI
UNIT Firene Archer Celine Level(16,Enemy,1) [2,29] 0x0 0x0 0x0 0x0 [SteelBow] PursueWithoutHeedAI
UNIT Firene Mage Celine Level(13,Enemy,1) [1,32] 0x0 0x0 0x0 0x0 [Meteor] NeverMoveAI
//Right room
UNIT Firene Warrior Celine Level(1,Enemy,1) [18,29] 0x0 0x0 0x0 0x0 [SteelAxe] PursueWithoutHeedAI
UNIT Firene Archer Celine Level(16,Enemy,1) [20,29] 0x0 0x0 0x0 0x0 [Longbow] PursueWithoutHeedAI
UNIT Firene Archer Celine Level(16,Enemy,1) [18,30] 0x0 0x0 0x0 0x0 [SteelBow] PursueWithoutHeedAI
UNIT Firene Mage Celine Level(13,Enemy,1) [21,31] 0x0 0x0 0x0 0x0 [Meteor] NeverMoveAI
//Guarding the throne room stairs
UNIT Firene Berserker Celine Level(3,Enemy,1) [3,20] 0x0 0x0 0x0 0x0 [KillerAxe] AttackInRangeAI
UNIT Firene Sniper Celine Level(3,Enemy,1) [4,19] 0x0 0x0 0x0 0x0 [SteelAxe] AttackInRangeAI
UNIT Firene Sage Celine Level(3,Enemy,1) [1,19] 0x0 0x0 0x0 0x0 [Elthunder] AttackInRangeAI
UNIT Firene KnightPaladin Celine Level(3,Enemy,1) [2,20] 0x0 0x0 0x0 0x0 [SilverSword] AttackInRangeAI
UNIT

BadThroneRoom:
UNIT Valencia KnightDuke Alm Level(3,Enemy,1) [5,4] 0x0 0x0 0x0 0x0 [IronSword] ChaseOnceApproachedAI
UNIT Valencia Sage Alm Level(3,Enemy,1) [6,6] 0x0 0x0 0x0 0x0 [Elfire] ChaseOnceApproachedAI
UNIT Valencia Hero Alm Level(3,Enemy,1) [7,8] 0x0 0x0 0x0 0x0 [SilverSword] ChaseOnceApproachedAI
UNIT Valencia General Alm Level(3,Enemy,1) [8,8] 0x0 0x0 0x0 0x0 [SilverLance] ChaseOnceApproachedAI
UNIT Valencia KnightBow Alm Level(3,Enemy,1) [9,8] 0x0 0x0 0x0 0x0 [SilverBow] ChaseOnceApproachedAI
UNIT Valencia KnightFalcon Alm Level(3,Enemy,1) [11,6] 0x0 0x0 0x0 0x0 [IronSword] ChaseOnceApproachedAI
UNIT Valencia Bishop_F Alm Level(3,Enemy,1) [12,4] 0x0 0x0 0x0 0x0 [Lightning, Physic] HealUnits
UNIT

LouisLives:
UNIT Louis General Celine Level(1,Enemy,0) [20,0] DropItem 0x0 0x1 LouisREDA [ShortSpear, KnightCrest] PursueWithoutHeedAI //ideal: move towards Celine, do not attack
UNIT Chloe KnightFalcon Celine Level(1,Enemy,0) [20,0] DropItem 0x0 0x1 ChloeREDA [SlimSword, ElysianWhip] PursueWithoutHeedAI //ideal: move towards Celine, do not attack
UNIT

Traps1:
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

BeginningScene:
CALL DismountAll
Text(Castle_Room,Ch16OpeningDoma)
Text(Castle_Room,Ch16OpeningGoodJobCeline)
Text(Castle_Hall,Ch16OpeningHowToStopAlm)
LOAD1 0x1 Good
LOAD1 0x1 Bad
LOAD1 0x1 BadDoma
LOAD1 0x1 BadTreasureRooms
LOAD1 0x1 BadThroneRoom
GotoPrepScreen
ENDA

AfterPrep:
Text(Castle_Room,Ch16AfterPrepEveryoneTakesPosition)
MOVE 10 Celine [2,16]
ENUN
Text(Ch16AfterPrepBeforeCelineWarps)
STAL 20
WARPOUT [2,16]
MOVE 0xFFFF Celine [20,6]
WARPIN [20,6]
ENDWARP
STAL 10
TILECHANGE 0xA //close the throne room stairs
//scene
ENDA

InitialWarpRagnarok:
//Shouldn't be able to kill Celine by T2
//She teleports on top of whoever's there, which is fine, I suppose.
//It's not a fatal issue and she moves immediately that enemy phase.
Text(Ch16CelineWarpsIn)
WARPOUT [20,6]
STAL 10
MOVE 0xFFFF Celine [11,20]
ENDWARP
Text(Ch16CelineSeesMarth)
ENDA

//Spawn Louis or Chloe in after a few turns if alive

//Killing the Doma rep triggers the throne room door to open

GreyStandsDown:
Text(Ch16GreyTurnsGreen)
TurnNPC(Grey)
NoFade
ENDA

MycenMoves:
Text(Ch16GreyPersuadesMycen)
TurnNPC(Mycen)
NoFade
ENDA

//SPARING CELINE:
//1: Time out the Ch6 defence and let Louis get away.
//2: Get 13x and do something involving Chloe.
//3: Do not kill any named Valencians, Cleive excepted: trying to talk him down is required.
//4: Let Louis/Chloe reach Celine this chapter (probably Louis); she'll turn green.
//5: With all that said and done, talk to her with Marth and she turns blue!

//Alm, at least, can be talked down with any Valencian.

LeftSwitchHit:
CALL SwitchCheck
ENDA

MiddleSwitchHit:
CALL SwitchCheck
ENDA

RightSwitchHit:
CALL SwitchCheck
ENDA

SwitchCheck:
//Hey, original Gaiden has that one map in Doma's Altar where the enemy hits the button.
//Let's bring that back, but this time you hit it. No flood of swamp this time, though.
CHECK_EVENTID 0x10 //left switch
BEQ 1601 s0 sC //if not hit, jump
CHECK_EVENTID 0x11 //middle switch
BEQ 1601 s0 sC //if not hit, jump
CHECK_EVENTID 0x12 //right switch
BEQ 1601 s0 sC //if not hit, jump
CAM1 [4,15] //the stairs
TILEREVERT 0xA //they're open!
//scene
LABEL 1601
NoFade
ENDA

EndingScene:
WM_LOADLOCATION2 0 15 0 //Required to fix chapter name
WM_SETDESTINATION 0 15 0 //World map node. Keep these equal.
WM_SETNEXTLOCATION 0 0 15 //Needed for world map narration.
MNCH 17
ENDA

LouisREDA:
REDA [20,2]

ChloeREDA:
REDA [20,1]