EventPointerTable(0x7, PointerList)

ALIGN 4
PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Good Good  //This unit group determines the number and position of deployment slots when there is a prep screen
POIN $0 $0 $0
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0x0,EntehMakesItOut,2)
TurnEventPlayer(0x0,GoblinSlayerSlain,3)
TurnEventPlayer(0x0,DogSpawn,5)
TurnEventPlayer(0x0,GiantSpawn,10)
TurnEventEnemy(0x0,LeftBehind,18)
END_MAIN

CharacterBasedEvents:
END_MAIN

LocationBasedEvents:
Armory(PlaceholderArmoury,16,27)
Escape(9,29) //the "take off lever"
END_MAIN

MiscBasedEvents:
CauseGameOverIfLordDies

END_MAIN

Dunno1:
END_MAIN

Dunno2:
END_MAIN

Dunno3:
END_MAIN

Tutorial:
END_MAIN

Good:
UNIT Sigurd FreeKnight 0x00 Level(1,Ally,0) [10,4] 0x0  0x0 0x0 0x0 [IronSword, SlimSword, Vulnerary] NoAI
UNIT Mario Lord 0x00 Level(1,Ally,0) [19,2] 0x0  0x0 0x0 0x0 [Mallet, DevilAxe, Vulnerary] NoAI
UNIT Mima Sage_F 0x00 Level(1,Ally,0) [9,3] 0x0  0x0 0x0 0x0 [Elfire, Lightning, StopSign] NoAI
UNIT Hornet Soldier 0x00 Level(1,Ally,0) [18,1] 0x0  0x0 0x0 0x0 [IronLance, Javelin] NoAI
UNIT

Bad:
//Boss: Wuxia Chicken. Dead guy spawns as a mask (zombie) fairly quickly. Old Birds flip between red and green every so often.
//Stretch goal: normal mode is a reasonable chapter and you can fight some of the enemies.
//Hard mode is truly Lethal Company: insta-kills everywhere and all enemies are ambush spawns.
UNIT Chicken Swordmaster 0x0 Level(1,Enemy,0) [11,27] 0x0 0x0 0x0 0x0 [IronSword] AttackInRangeAI
UNIT OldBird1 Sniper 0x00 Level(1,Enemy,0) [19,19] 0x0  0x0 0x0 0x0 [Ballista] AttackInRangeAI
UNIT OldBird2 Sniper 0x00 Level(1,Enemy,0) [6,20] 0x0  0x0 0x0 0x0 [BraveBow] ChaseOnceApproachedAI
UNIT OldBird3 Sniper 0x00 Level(1,Enemy,0) [15,17] 0x0  0x0 0x0 0x0 [Ballista] ChaseOnceApproachedAI
UNIT Giant Cyclops 0x00 Level(1,Enemy,0) [9,22] 0x0  0x0 0x0 0x0 [IronAxe] ChaseOnceApproachedAI
UNIT Giant Cyclops 0x00 Level(1,Enemy,0) [2,15] 0x0  0x0 0x0 0x0 [IronAxe] AttackInRangeAI
UNIT Dog Gwyllgi 0x00 Level(1,Enemy,0) [22,7] 0x0  0x0 0x0 0x0 [FireFang] AttackInRangeAI
UNIT Dog Gwyllgi 0x00 Level(1,Enemy,0) [12,4] 0x0  0x0 0x0 0x0 [FireFang] PursueWithoutHeedAI
UNIT BaboonHawk Brigand 0x00 Level(1,Enemy,0) [7,16] 0x0  0x0 0x0 0x0 [IronAxe] ChaseOnceApproachedAI
UNIT BaboonHawk Brigand 0x00 Level(1,Enemy,0) [15,14] 0x0  0x0 0x0 0x0 [IronAxe] ChaseOnceApproachedAI
UNIT BaboonHawk Brigand 0x00 Level(1,Enemy,0) [4,17] 0x0  0x0 0x0 0x0 [IronAxe] ChaseOnceApproachedAI
UNIT BaboonHawk Brigand 0x00 Level(1,Enemy,0) [16,22] 0x0  0x0 0x0 0x0 [IronAxe] ChaseOnceApproachedAI
UNIT

EntehGroup:
UNIT Enteh Cleric 0x00 Level(1,Ally,0) [9,4] 0x0 0x0 0x0 0x0 [Heal, TorchStaff] NoAI
UNIT

MaskedGoblin:
UNIT GoblinSlayer Revenant 0x00 Level(1,Enemy,0) [9,3] 0x0 0x0 0x1 GoblinREDA [SharpClaw] PursueWithoutHeedAI
UNIT

GoblinREDA:
REDA [9,5]

T5Spawn:
UNIT Dog Gwyllgi 0x00 Level(1,Enemy,0) [11,11] 0x0  0x0 0x0 0x0 [FireFang] AttackInRangeAI
UNIT

T10Spawn:
UNIT Giant Cyclops 0x00 Level(1,Enemy,0) [9,8] 0x0  0x0 0x0 0x0 [IronAxe] PursueWithoutHeedAI
UNIT

Traps1:
Scrap(10,5) //Test
Scrap(2,3) //Hangar exit
Scrap(6,8) //Foggy hangar
Scrap(9,16) //Main road
Scrap(3,20) //Southwest hangar
Scrap(22,5) //Fence hole
Scrap(21,22) //Southeast corner
Scrap(17,17) //Southeast hangar
Scrap(11,25) //Hill
Scrap(16,29) //Ship
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

BeginningScene:
//MUSC M_Ricardo
MUSC M_Slider
Text(Riverside,LethalObligatoryETCBanterMario)
MUSC M_BirthOfTheHolyKnight
Text(Riverside,LethalObligatoryETCBanterFE)
MUSC M_SimonsTheme
//no Yugioh music in the repo, use a different Konami series
Text(Riverside,LethalObligatoryETCBanterYGO)
MUSC M_TheDreadfulFight
Text(Riverside,LethalObligatoryETCBanterFF)
//MUSC M_WhenTheRushComes
MUSC M_FinalDestination
Text(Riverside,LethalObligatoryETCBanterSmash)
MUSC M_MetropolisZone
Text(Riverside,LethalObligatoryETCBanterSonic)
MUSC M_Ginza
Text(Riverside,LethalObligatoryETCBanterSMT)
MUSC M_SeptetteForTheDeadPrincess
Text(Riverside,LethalObligatoryETCBanterTouhou)
//Switch tracks again, if you let Yukari brag about borders Necrofantasia would be nice
Text(Town_Gate,LethalReturnToFoyleRoad)
//MUSC M_BattleForWhoseSake
MUSC M_RedBrinstar
Text(Fort_Outside,LethalSpacePirateMarch)
MUSC M_InTheFinal
Text(Fort_Outside,LethalFawfulWins)
//MUSC M_ScarsOfTheScouring
MUSC M_WhenTheRushComes
SetBackground(CGBlackScreen)
TUTORIALTEXTBOXSTART
CenterTutorialTextBox
TEXTSHOW LethalLogQuota1
TEXTEND
STAL 30
TUTORIALTEXTBOXSTART
CenterTutorialTextBox
TEXTSHOW LethalLogQuota2
TEXTEND
STAL 30
TUTORIALTEXTBOXSTART
CenterTutorialTextBox
TEXTSHOW LethalLogQuota3
TEXTEND
STAL 30
TUTORIALTEXTBOXSTART
CenterTutorialTextBox
TEXTSHOW LethalLogQuota4
TEXTEND
STAL 30
TUTORIALTEXTBOXSTART
CenterTutorialTextBox
TEXTSHOW LethalLogQuota5
TEXTEND
STAL 30
TUTORIALTEXTBOXSTART
CenterTutorialTextBox
TEXTSHOW LethalLogQuota6
TEXTEND
STAL 30
TUTORIALTEXTBOXSTART
CenterTutorialTextBox
TEXTSHOW LethalLogQuota7
TEXTEND
STAL 30
TUTORIALTEXTBOXSTART
CenterTutorialTextBox
TEXTSHOW LethalLogQuota8
TEXTEND
STAL 30
TUTORIALTEXTBOXSTART
CenterTutorialTextBox
TEXTSHOW LethalLogQuota9
TEXTEND
STAL 30
TUTORIALTEXTBOXSTART
CenterTutorialTextBox
TEXTSHOW LethalLogQuota10
TEXTEND
STAL 30
TUTORIALTEXTBOXSTART
CenterTutorialTextBox
TEXTSHOW LethalPlanetIntroduction
TEXTEND
STAL 30
CALL $9EE2C4 //EndConvo, get rid of black screen
MUSC M_OffToWar
Text(Shrine_lighter,LethalPreArtificeBriefing)
MUSC M_Companions
Text(Castle_Hall_4,LethalProperPlatforming)
Text(Castle_Hall_4,LethalMineMishap)
Text(Castle_Hall_4,LethalSnareSurvival)
Text(Castle_Hall_4,LethalBrackenBreak)
Text(Castle_Hall_4,LethalDodgingDanmaku)
Text(Castle_Hall_4,LethalMaskMauling)
MUSC M_WarringPowers
Text(Castle_Hall_4,LethalTheJesterAttacks)
TILECHANGE 0x1
TILECHANGE 0x2 //close top two hangars
LOAD1 0x1 Good
LOAD1 0x1 Bad
CAM1 Sigurd
Text(LethalMainEntrance)
CAM1 Mario
Text(LethalFireExit)
TUTORIALTEXTBOXSTART
CenterTutorialTextBox
TEXTSHOW LethalTutorial
TEXTEND
STAL 30

ENDA

EndingScene:
Text(LethalPrepareToTakeOff)
//got 5k? great, continue. Otherwise, die.
SVAL s1 5000
CHECK_MONEY
BGT 6899 s1 sC
MUSC M_IntoTheShadowOfTriumph
Text(Shrine_lighter,LethalEndingInSpaceQuota)
MNTS 0x0
GOTO 6898
LABEL 6899
MUSC M_EvilOnes
Text(Shrine_lighter,LethalEndingDisciplinaryProcedures)
GameOver
LABEL 6898
ENDA

EntehMakesItOut:
Text(LethalT2EntehEscapes)
LOAD1 0x1 EntehGroup
ENDA

GoblinSlayerSlain:
//LOAD1 0x1 MaskedGoblin
//CAM1 MaskVictim
//text
//STAL 10
//too early, push him back
FOGVISION 10
CHECK_ALIVE OldBird1
BEQ 6850 s0 sC
TurnNPC(OldBird1)
LABEL 6850
ENDA

DogSpawn:
LOAD1 0x1 MaskedGoblin
CAM1 MaskVictim
//text
STAL 10
LOAD1 0x1 T5Spawn
CHECK_ALIVE OldBird1
BEQ 6851 s0 sC
TurnEnemy(OldBird1)
LABEL 6851
CHECK_ALIVE OldBird3
BEQ 6852 s0 sC
TurnNPC(OldBird3)
LABEL 6852
FOGVISION 8
ENDA

GiantSpawn:
LOAD1 0x1 T10Spawn
CHECK_ALIVE OldBird3
BEQ 6853 s0 sC
TurnEnemy(OldBird3)
LABEL 6853
CHECK_ALIVE OldBird2
BEQ 6854 s0 sC
TurnNPC(OldBird2)
LABEL 6854
FOGVISION 6
ENDA

HurryUpRain:
WEATHER 4 //rain, because it's flooded Artifice. Good luck.
ENDA

LeftBehind:
TUTORIALTEXTBOXSTART
CenterTutorialTextBox
TEXTSHOW LethalLeftBehind
TEXTEND
STAL 30
GameOver
ENDA

PlaceholderArmoury:
SHLI Tyrfing
ALIGN 4