EventPointerTable(0x33, PointerList)

ALIGN 4
PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Good Good  //This unit group determines the number and position of deployment slots when there is a prep screen
POIN $0 $0 $0
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0x0,AfterBattlePrep,1)
TurnEventEnemy(0x0,InfiniteRangeBlazingLion,1)
TurnEventPlayer(0x0,WorthlessArsonist,2)
TurnEventEnemy(0x0,LoadSouthernReinforcements,4)
TurnEventEnemy(0x0,LoadSouthernReinforcements2,5)
TurnEventNPC(0x0,DownGoTheSideGates,6)
TurnEventEnemy(0x0,LoadDragonKnights,8)
TurnEventEnemy(0x0,DiamantsCharge,9)
TurnEventNPC(0x3,EndingScene,12)
END_MAIN

CharacterBasedEvents:
END_MAIN

LocationBasedEvents:
Village(0x7,Village1515,15,15)
Village(0x8,Village1915,19,15)
Village(0x9,Village118,1,18)
Village(0xa,Village1318,13,18)
Village(0xb,Village1521,15,21)
Village(0xc,Village1921,19,21)
Village(0xd,Village124,1,24)
Village(0xe,Village1324,13,24)
Chest(PureWater,18,1)
Chest(Vulnerary,20,1)
Door(4,8) //main gate
Door(17,3) //treasure room
Vendor(Ch11Vendor,2,12)
Armory(Ch11WestArmoury,14,10)
Armory(Ch11EastArmoury,19,11)
#ifdef __DEBUG__
Seize(0x3,EndingScene,5,6)
#endif
END_MAIN

MiscBasedEvents:
//Later version: Make the throne room falling a lose condition
CauseGameOverIfLordDies
DefeatBoss(EndingScene)
END_MAIN

Dunno1:
END_MAIN

Dunno2:
END_MAIN

Dunno3:
END_MAIN

Tutorial:
END_MAIN

Good:
UNIT Marth StarLord 0x00 Level(1,Ally,0) [5,6] 0x0  0x0 0x0 0x0 [SilverSword, Vulnerary] NoAI
UNIT Lilina Mage_F 0x00 Level(1,Ally,0) [5,1] 0x0 0x0 0x0 0x0 [Fire, Vulnerary] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [3,6] 0x0 0x0 0x0 0x0 [SilverLance, Vulnerary] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [7,6] 0x0 0x0 0x0 0x0 [SilverLance, Vulnerary] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [14,5] 0x0 0x0 0x0 0x0 [SilverLance, Vulnerary] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [19,5] 0x0 0x0 0x0 0x0 [SilverLance, Vulnerary] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [13,3] 0x0 0x0 0x0 0x0 [SilverLance, Vulnerary] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [12,6] 0x0 0x0 0x0 0x0 [SilverLance, Vulnerary] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [1,5] 0x0 0x0 0x0 0x0 [SilverLance, Vulnerary] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [4,2] 0x0 0x0 0x0 0x0 [SilverLance, Vulnerary] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [6,2] 0x0 0x0 0x0 0x0 [SilverLance, Vulnerary] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [17,2] 0x0 0x0 0x0 0x0 [SilverLance, Vulnerary] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [19,1] 0x0 0x0 0x0 0x0 [SilverLance, Vulnerary] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [9,4] 0x0 0x0 0x0 0x0 [SilverLance, Vulnerary] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [4,7] 0x0 0x0 0x0 0x0 [SilverLance, Vulnerary] NoAI
UNIT

BarthAndCompany:
UNIT Barth General 0x00 Level(5,NPC,0) [4,9] 0x00 0x0 0x0 0x0 [SilverLance, HandAxe, Elixir] NoAI
UNIT Ostia LanceArmour Barth Level(10,NPC,1) [5,9] 0x0 0x0 0x0 0x0 [SteelLance] GuardTileAI
UNIT Ostia LanceArmour Barth Level(10,NPC,1) [16,8] 0x0 0x0 0x0 0x0 [SteelLance] GuardTileAI
UNIT

Bad:
UNIT Diamant MasterLord 0x00 Level(3,Enemy,0) [8,25] DropItem 0x0 0x0 0x0 [SilverSword, EarthOrb, RoyRing] AttackInRangeAI
UNIT Brodia Mercenary Diamant Level(10,Enemy,1) [6,23] 0x0 0x0 0x0 0x0 [SteelSword] AttackInRangeAI
UNIT Brodia Soldier Diamant Level(10,Enemy,1) [7,24] 0x0 0x0 0x0 0x0 [SteelLance] AttackInRangeAI
UNIT Brodia Myrmidon Diamant Level(12,Enemy,1) [8,22] 0x0 0x0 0x0 0x0 [KillingEdge] AttackInRangeAI
UNIT Brodia Fighter Diamant Level(10,Enemy,1) [9,24] 0x0 0x0 0x0 0x0 [SteelAxe] AttackInRangeAI
UNIT Brodia Mercenary Diamant Level(10,Enemy,1) [10,23] 0x0 0x0 0x0 0x0 [SteelSword] AttackInRangeAI
UNIT Brodia SwordArmour Diamant Level(10,Enemy,1) [13,25] 0x0 0x0 0x0 0x0 [SteelSword] NeverMoveAI
UNIT Brodia LanceArmour Diamant Level(12,Enemy,1) [17,22] 0x0 0x0 0x0 0x0 [Javelin] NeverMoveAI
UNIT Brodia BowArmour Diamant Level(10,Enemy,1) [15,19] DropItem 0x0 0x0 0x0 [Longbow, SteelBow] NeverMoveAI
UNIT Brodia Mage Diamant Level(10,Enemy,1) [7,14] 0x0 0x0 0x0 0x0 [Elfire] PursueWithoutHeedAI
UNIT Brodia Shaman Diamant Level(10,Enemy,1) [12,16] 0x0 0x0 0x0 0x0 [Flux] PursueWithoutHeedAI
UNIT Brodia Monk Diamant Level(10,Enemy,1) [3,14] 0x0 0x0 0x0 0x0 [Shine] PursueWithoutHeedAI
UNIT Brodia Archer Diamant Level(10,Enemy,1) [4,12] 0x0 0x0 0x0 0x0 [SteelBow] PursueWithoutHeedAI
UNIT Brodia Mage Diamant Level(10,Enemy,1) [20,22] 0x0 0x0 0x0 0x0 [Meteor] NeverMoveAI
UNIT Brodia Archer Diamant Level(10,Enemy,1) [1,25] 0x0 0x0 0x0 0x0 [Ballista] NeverMoveAI
UNIT Brodia Mage Diamant Level(10,Enemy,1) [10,11] 0x0 0x0 0x0 0x0 [Elwind] PursueWithoutHeedAI
UNIT

Bad2:
UNIT Colonial LanceKnight 0x0 Level(10,Enemy,1) [10,20] 0x0 0x0 0x0 0x0 [SteelLance] ChaseOnceApproachedAI
UNIT Colonial AxeKnight 0x0 Level(10,Enemy,1) [9,21] 0x0 0x0 0x0 0x0 [SteelAxe] ChaseOnceApproachedAI
UNIT Colonial FreeKnight 0x0 Level(10,Enemy,1) [11,21] 0x0 0x0 0x0 0x0 [Armourslayer] ChaseOnceApproachedAI
UNIT Colonial LanceKnight 0x0 Level(10,Enemy,1) [10,20] 0x0 0x0 0x0 0x0 [SteelLance] ChaseOnceApproachedAI
UNIT Colonial LanceKnight 0x0 Level(12,Enemy,1) [16,12] 0x0 0x0 0x0 0x0 [SilverLance] PursueWithoutHeedAI
UNIT Colonial LanceKnight 0x0 Level(10,Enemy,1) [15,12] 0x0 0x0 0x0 0x0 [Javelin] PursueWithoutHeedAI
UNIT Colonial FreeKnight 0x0 Level(10,Enemy,1) [17,12] 0x0 0x0 0x0 0x0 [SteelSword] PursueWithoutHeedAI
UNIT Colonial LanceKnight 0x0 Level(10,Enemy,1) [18,12] 0x0 0x0 0x0 0x0 [Horseslayer] PursueWithoutHeedAI
UNIT Colonial Troubadour 0x0 Level(10,Enemy,1) [16,13] 0x0 0x0 0x0 0x0 [Mend, RedGem] HealUnits
UNIT Colonial AxeKnight 0x0 Level(10,Enemy,1) [2,19] 0x0 0x0 0x0 0x0 [HandAxe] PursueWithoutHeedAI
UNIT Colonial ArchKnight 0x0 Level(10,Enemy,1) [4,21] 0x0 0x0 0x0 0x0 [ShortBow] PursueWithoutHeedAI
UNIT Colonial Troubadour 0x0 Level(10,Enemy,1) [3,24] 0x0 0x0 0x0 0x0 [Physic] HealUnits
UNIT RogueBern DragonKnight 0x0 Level(10,Enemy,1) [17,19] 0x0 0x0 0x0 0x0 [ShortSpear] AttackInRangeAI
UNIT RogueBern DragonKnight 0x0 Level(10,Enemy,1) [20,12] 0x0 0x0 0x0 0x0 [IronLance] PursueWithoutHeedAI
UNIT RogueBern General 0x0 Level(1,Enemy,1) [6,17] 0x0 0x0 0x0 0x0 [Javelin] NeverMoveAI
UNIT RogueBern DragonKnight 0x0 Level(10,Enemy,1) [19,6] 0x0 0x0 0x0 0x0 [IronLance] PursueWithoutHeedAI
UNIT RogueBern DragonKnight 0x0 Level(10,Enemy,1) [0,10] 0x0 0x0 0x0 0x0 [SteelLance] PursueWithoutHeedAI
UNIT

AstoreGroup:
UNIT Astore Thief 0x0 Level(10,Ally,0) [13,18] 0x0 0x0 0x1 AstoreREDA [SteelSword, PoisonSword, Vulnerary] NoAI
UNIT

SouthernReinforcements:
UNIT Colonial LanceKnight 0x0 Level(10,Enemy,1) [6,27] 0x0 0x0 0x1 SouthREDA1 [Javelin] PursueWithoutHeedAI
UNIT Colonial LanceKnight 0x0 Level(10,Enemy,1) [7,27] 0x0 0x0 0x1 SouthREDA2 [SteelLance] PursueWithoutHeedAI
UNIT Colonial LanceKnight 0x0 Level(10,Enemy,1) [8,27] 0x0 0x0 0x1 SouthREDA3 [IronLance] PursueWithoutHeedAI
UNIT

SouthernReinforcements2:
UNIT Colonial FreeKnight 0x0 Level(10,Enemy,1) [6,27] 0x0 0x0 0x1 SouthREDA1 [ThunderSword] PursueWithoutHeedAI
UNIT Colonial AxeKnight 0x0 Level(10,Enemy,1) [7,27] 0x0 0x0 0x1 SouthREDA2 [IronAxe] PursueWithoutHeedAI
UNIT Colonial ArchKnight 0x0 Level(10,Enemy,1) [8,27] 0x0 0x0 0x1 SouthREDA3 [IronBow] PursueWithoutHeedAI
UNIT

WendyFriendsAndFoesEast:
UNIT Wendy LanceArmour 0x0 Level(10,NPC,0) [20,6] 0x0 0x0 0x1 WendyREDA [SteelLance, Javelin] GuardTileAI
UNIT Ostia LanceArmour 0x0 Level(10,NPC,1) [20,6] 0x0 0x0 0x1 WendyEastAllyREDA [IronLance, Javelin] GuardTileAI
UNIT Brodia Fighter 0x0 Level(10,Enemy,1) [20,5] 0x0 0x0 0x1 WendyEastEnemyREDA1 [Hammer] PursueWithoutHeedAI
UNIT Brodia Mage 0x0 Level(10,Enemy,1) [20,5] 0x0 0x0 0x1 WendyEastEnemyREDA2 [Wind] PursueWithoutHeedAI
UNIT

WendyFriendsAndFoesWest:
UNIT Ostia AxeArmour 0x0 Level(10,NPC,1) [0,5] 0x0 0x0 0x1 WendyWestAllyREDA1 [SteelAxe, HandAxe] GuardTileAI
UNIT Ostia AxeArmour 0x0 Level(10,NPC,1) [0,6] 0x0 0x0 0x1 WendyWestAllyREDA2 [IronAxe, HandAxe] GuardTileAI
UNIT Brodia Mercenary 0x0 Level(10,Enemy,1) [0,6] 0x0 0x0 0x1 WendyWestEnemyREDA1 [Armourslayer] PursueWithoutHeedAI
UNIT Brodia Monk 0x0 Level(10,Enemy,1) [0,5] 0x0 0x0 0x1 WendyWestEnemyREDA2 [Shine] PursueWithoutHeedAI
UNIT

RogueFlyersEast:
UNIT RogueBern DragonKnight 0x0 Level(10,Enemy,1) [20,25] 0x0 0x0 0x1 DragonREDA1 [IronLance] PursueWithoutHeedAI
UNIT RogueBern DragonKnight 0x0 Level(10,Enemy,1) [20,16] 0x0 0x0 0x1 DragonREDA2 [IronLance] PursueWithoutHeedAI
UNIT

RogueFlyersSouth:
UNIT RogueBern DragonKnight 0x0 Level(10,Enemy,1) [5,27] 0x0 0x0 0x1 DragonREDA3 [SteelLance] PursueWithoutHeedAI
UNIT RogueBern DragonKnight 0x0 Level(10,Enemy,1) [9,27] 0x0 0x0 0x1 DragonREDA4 [Javelin] PursueWithoutHeedAI
UNIT

CutsceneAlear:
UNIT Alear DivineOne 0x0 Level(20,Enemy,0) [7,27] 0x0 0x0 0x1 AlearT9REDA [SilverSword] PursueWithoutHeedAI
//Ideally her actual Falchion equivalent - not for v0.3!
UNIT

CutsceneBarthGate:
UNIT Barth General 0x00 Level(5,NPC,0) [4,9] 0x00 0x0 0x0 0x0 [SilverLance, HandAxe, Elixir] NoAI
UNIT Wendy LanceArmour 0x0 Level(10,NPC,0) [5,9] 0x0 0x0 0x0 0x0 [SteelLance, Javelin] GuardTileAI
UNIT Alfonse Hero 0x00 Level(20,NPC,0) [4,10] 0x00 0x0 0x0 0x0 [Falchion] NoAI
UNIT Lilina Mage_F 0x00 Level(10,Ally,0) [4,21] 0x0 0x0 0x0 0x0 [Fire] NoAI
UNIT Eliwood Paladin 0x00 Level(1,Ally,0) [4,22] 0x0 0x0 0x0 0x0 [SilverSword] NoAI
UNIT

CutsceneSiegePlanning:
UNIT Barth General 0x00 Level(5,NPC,0) [7,18] 0x00 0x0 0x0 0x0 [SilverLance, HandAxe, Elixir] NoAI
UNIT Lilina Mage_F 0x00 Level(10,Ally,0) [6,17] 0x0 0x0 0x0 0x0 [Fire] NoAI
UNIT Eliwood KnightPaladin 0x00 Level(1,Ally,0) [5,18] 0x0 0x0 0x0 0x0 [SilverSword] NoAI
UNIT

CutsceneSleepingUnits:
UNIT Marth Lord 0x00 Level(1,Ally,0) [11,10] 0x0 0x0 0x0 0x0 [Rapier] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [10,9] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT Chiki Mamkute_F 0x00 Level(1,Ally,0) [8,9] 0x0 0x0 0x0 0x0 [Vulnerary] NoAI
UNIT Lilina Mage_F 0x00 Level(10,Ally,0) [14,14] 0x0 0x0 0x0 0x0 [Fire] NoAI
UNIT

CutsceneDiamantDemands:
UNIT Lilina Mage_F 0x00 Level(10,Ally,0) [4,6] 0x0 0x0 0x0 0x0 [Fire] NoAI
UNIT Marth Lord 0x00 Level(1,Ally,0) [3,7] 0x0 0x0 0x0 0x0 [Rapier] NoAI
UNIT Diamant MasterLord 0x00 Level(3,Enemy,0) [9,16] DropItem 0x0 0x0 0x0 [SilverSword/*, RoyRing, EarthOrb*/] GuardTileAI
UNIT Ch7Boss Cavalier 0x00 Level(1,Enemy,0) [9,17] 0x0 0x0 0x0 0x0 [IronSword] NoAI //Arcard Jr is unfought, so no ID
UNIT Barth General 0x00 Level(5,NPC,0) [4,9] 0x00 0x0 0x0 0x0 [SilverLance, HandAxe, Elixir] NoAI
UNIT

CutsceneWendyT1:
UNIT Wendy LanceArmour 0x0 Level(10,NPC,0) [17,6] 0x0 0x0 0x0 0x0 [SteelLance, Javelin] GuardTileAI
UNIT

CutsceneEndingOutside:
UNIT Barth General 0x00 Level(5,Ally,0) [4,12] 0x00 0x0 0x0 0x0 [SilverLance, HandAxe, Elixir] NoAI
UNIT Lilina Mage_F 0x00 Level(10,Ally,0) [5,10] 0x0 0x0 0x0 0x0 [Fire] NoAI
UNIT Eliwood KnightPaladin 0x00 Level(1,Ally,0) [5,11] 0x0 0x0 0x0 0x0 [SilverSword] NoAI
UNIT Marth Lord 0x00 Level(1,Ally,0) [6,10] 0x0 0x0 0x0 0x0 [Rapier] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [6,8] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT Chiki Mamkute_F 0x00 Level(1,Ally,0) [7,9] 0x0 0x0 0x0 0x0 [Vulnerary] NoAI
UNIT Alear DivineOne 0x0 Level(20,Enemy,0) [7,13] 0x0 0x0 0x0 0x0 [SilverSword] PursueWithoutHeedAI
UNIT

CutsceneEndingInside:
UNIT Barth General 0x00 Level(5,Ally,0) [10,2] 0x00 0x0 0x0 0x0 [SilverLance, HandAxe, Elixir] NoAI
UNIT Lilina Mage_F 0x00 Level(10,Ally,0) [9,2] 0x0 0x0 0x0 0x0 [Fire] NoAI
UNIT Eliwood KnightPaladin 0x00 Level(1,Ally,0) [9,3] 0x0 0x0 0x0 0x0 [SilverSword] NoAI
UNIT Marth Lord 0x00 Level(1,Ally,0) [8,4] 0x0 0x0 0x0 0x0 [Rapier] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [8,5] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT Chiki Mamkute_F 0x00 Level(1,Ally,0) [7,4] 0x0 0x0 0x0 0x0 [Vulnerary] NoAI
UNIT Alear DivineOne 0x0 Level(20,Enemy,0) [22,4] 0x0 0x0 0x0 0x0 [SilverSword] PursueWithoutHeedAI
UNIT

CutsceneEndingInsideNoBarth:
UNIT Lilina Mage_F 0x00 Level(10,Ally,0) [9,2] 0x0 0x0 0x0 0x0 [Fire] NoAI
UNIT Eliwood KnightPaladin 0x00 Level(1,Ally,0) [9,3] 0x0 0x0 0x0 0x0 [SilverSword] NoAI
UNIT Marth Lord 0x00 Level(1,Ally,0) [8,4] 0x0 0x0 0x0 0x0 [Rapier] NoAI
UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [8,5] 0x0 0x0 0x0 0x0 [WingSpear] NoAI
UNIT Chiki Mamkute_F 0x00 Level(1,Ally,0) [7,4] 0x0 0x0 0x0 0x0 [Vulnerary] NoAI
UNIT Alear DivineOne 0x0 Level(20,Enemy,0) [22,4] 0x0 0x0 0x0 0x0 [SilverSword] PursueWithoutHeedAI
UNIT


CutsceneEndingDurandal:
UNIT Lilina Mage_F 0x00 Level(10,Ally,0) [23,25] 0x0 0x0 0x1 LilinaREDA [Fire] NoAI
UNIT Eliwood KnightPaladin 0x00 Level(1,Ally,0) [24,25] 0x0 0x0 0x1 EliwoodREDA [SilverSword] NoAI
//UNIT Marth Lord 0x00 Level(1,Ally,0) [24,25] 0x0 0x0 0x1 MarthREDA [Rapier] NoAI
//UNIT Sheeda PegasusKnight 0x00 Level(1,Ally,0) [25,25] 0x0 0x0 0x1 SheedaREDA [WingSpear] NoAI
//UNIT Chiki Mamkute_F 0x00 Level(1,Ally,0) [23,25] 0x0 0x0 0x1 ChikiREDA [Vulnerary] NoAI
UNIT

CutsceneEndingBad:
UNIT Alear DivineOne 0x0 Level(20,Enemy,0) [6,21] 0x0 0x0 0x0 0x0 [SilverSword] PursueWithoutHeedAI
UNIT Framme Bishop_F 0x0 Level(1,Enemy,0) [7,22] 0x0 0x0 0x0 0x0 [Recover] NoAI
UNIT Alfonse Hero 0x00 Level(20,Enemy,0) [5,23] 0x00 0x0 0x0 0x0 [Falchion] NoAI
UNIT Veronica Druid_F 0x00 Level(20,Enemy,0) [4,22] 0x0 0x0 0x0 0x0 [Maph] NoAI
UNIT

Traps1:
SavePoint(11,5)
SavePoint(9,17)
//So there's FIRE and FIRE2, and this isn't Final Fantasy.
//FIRE activates every x turns (can be set).
//FIRE2 is a case of "step on it, get burnt"?
FIRE2 [15,7]
FIRE2 [17,7]
FIRE2 [16,8]
FIRE2 [9,12]
FIRE2 [14,12]
FIRE2 [6,13]
FIRE2 [12,16]
FIRE2 [4,17]
FIRE2 [15,7]
FIRE2 [7,19]
FIRE2 [15,7]
FIRE2 [14,19]
FIRE2 [6,21]
FIRE2 [2,22]
FIRE2 [8,22]
FIRE2 [11,22]
FIRE2 [17,24]
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

BeginningScene:
MUSC M_AnUnexpectedCaller
LOAD2 0x1 CutsceneBarthGate
CAM1 [5,8] //the gate
CALL MountEliwood //he should ride in here
FADU 10
FlashCursor(Barth,60)
Text(Castle_Outside,Ch11OpeningEntry)
MOVE 10 Alfonse [0,14]
ENUN
DISA Alfonse
MOVE 10 Eliwood [5,10]
MOVE 10 Lilina [4,10]
ENUN
FlashCursor(Barth,60)
Text(Castle_Outside,Ch11OpeningEntry2)
SOUN 0xB1 //Door opening
TILECHANGE 9 //the front door
STAL 10
MOVE 10 Barth [4,7]
MOVE 10 Eliwood [5,5]
MOVE 10 Lilina [4,5]
ENUN
CALL DismountEliwood
TILEREVERT 9
STAL 10
MOVE 10 Barth [0,0]
MOVE 10 Eliwood [0,0]
MOVE 10 Lilina [0,0]
ENUN
FADI 10
CLEA 0
CLEN 0
CLEE 0
LOMA 0x43 //Ostia Castle
LOAD2 0x0 CutsceneSiegePlanning
CAM1 [6,17] //the throne - Lilina's sitting on it
FADU 10
FlashCursor(Eliwood,60)
Text(Castle_Room,Ch11OpeningPolitics)
STAL 10
FADI 10
MUSC 0x7FFF //silence
CLEA 0
CLEN 0
LOAD2 0x0 CutsceneSleepingUnits
CALL DismountSheeda //10x has outdoor sections, so dismount her again
CAM1 Marth //he's sleeping in the treasure room: it was there or the cell
STAL 30
FADU 10
//"The next morning" text box
MOVE 10 Lilina [14,10]
ENUN
Text(Castle_Hall_3,Ch11OpeningDiamantIsOutside)
MOVE 10 Sheeda [9,9] //wake Chiki up
MOVE 10 Marth [12,10] //the door
ENUN
SOUN 0xB1 //Door opening
TILECHANGE 0xB //the relevant door
STAL 10
MOVE 10 Marth [14,11] 
ENUN
MOVE 10 Marth [17,23]
MOVE 10 Lilina [17,24]
ENUN
FADI 10
CLEA 0
LOMA 11 //back outside
LOAD2 0x1 CutsceneDiamantDemands
CAM1 [5,8] //the gate
TILECHANGE 9 //the front door. Should handwave this: open it to see Diamant, then malfunctions?
FADU 10
MUSC M_AnUnexpectedCaller
STAL 10
MOVE 8 Diamant [4,10]
MOVE 8 Ch7Boss [5,10]
ENUN
FlashCursor(Diamant,60)
Text(Town_Gate,Ch11OpeningDiamantDemands)
FADI 10
CLEA 0
CLEE 0
CLEN 0
STAL 10
LOAD3 0x1 Good
LOAD1 0x1 Bad
LOAD1 0x1 Bad2
LOAD1 0x1 BarthAndCompany
TILECHANGE 9 //the front door
CALL MountAll //still a few indoor tiles, but this map is 95% outdoors as opposed to the ~50% of 10x
MUSC M_EnemiesAppear
CALL MountEliwood
CALL MountSheeda
CAM1 [0,0]
FADU 10
STAL 20
CAM1 [20,0]
STAL 20
CAM1 [20,27]
STAL 20
CAM1 [0,27]
STAL 20
CAM1 [0,0]
STAL 10
GotoPrepScreen
ENDA

EndingScene:
//Conditional for dead Diamant - mandatory for true ending
CHECK_EVENTID 0x2 //did you get the boss kill?
BEQ 1101 s0 sC //branch if alive
ENUT DiamantDead
MUSC M_Triumph
Text(Ch11EndingAlearIfDiamantDies)
CAM1 Marth
MUSC M_IntoTheShadowOfTriumph
STAL 10
FlashCursor(Marth,60)
Text(Ch11EndingWeGotTheEarthOrb)
//Can't have multiple items drop like in FE3, so...
SVAL s3 EarthOrb
GIVEITEMTO Marth
GOTO 1102
LABEL 1101

//Diamant lives
MUSC M_IntoTheShadowOfTriumph
Text(Ch11EndingAlearIfTimerWaitedOut)
Text(Ch11EndingHowMuchLonger)
LABEL 1102

FADI 10
CLEA 0
CLEE 0
CLEN 0
LOAD2 0x0 CutsceneEndingOutside
CALL MountSheeda //she's sitting on a wall, needs to be mounted
CALL MountEliwood
CAM1 [6,10] //near the gate
FADU 10

CHECK_EVENTID NoSummonGun
BNE 1110 s0 sC
MUSC M_JoinUs
CALL UnitGachaAct2
WARPIN [5,6]
MOVE 0xFFFF 0xFFFD [5,6]
ENUN
ENDWARP
LABEL 1110
MUSC M_IntoTheShadowOfTriumph
MOVE 10 Alear [6,13]
ENUN
FlashCursor(Alear,60)
Text(Outside_Village_Day,Ch11EndingTheDivineReunion)
CHECK_EVENTID DiamantDead
BEQ 1103 s0 sC
Text(Outside_Village_Day,Ch11EndingAlearChargeDiamantDead)
GOTO 1104
LABEL 1103
Text(Outside_Village_Day,Ch11EndingAlearChargeDiamantAlive)
LABEL 1104
FlashCursor(Barth,60)
Text(Outside_Village_Day,Ch11EndingEveryoneFallBack)
MOVE 10 Marth [4,5]
MOVE 10 Sheeda [5,5]
MOVE 10 Chiki [6,5]
MOVE 10 Eliwood [4,6]
MOVE 10 Lilina [5,6]
MOVE 10 Barth [6,6]
ENUN
CALL DismountEliwood
CALL DismountSheeda
MOVE 10 Marth [9,0]
MOVE 10 Sheeda [10,0]
MOVE 10 Chiki [10,1]
MOVE 10 Eliwood [9,0]
MOVE 10 Lilina [10,0]
MOVE 10 Barth [10,1]
MOVE 10 Alear [5,1]
ENUN
FADI 10
CLEA 0
CLEE 0
CLEN 0

LOMA 0x43 //Ostia Castle
CHECK_ALIVE Barth
BEQ 1120 s0 sC //If Barth is dead, branch
LOAD2 0x0 CutsceneEndingInside
SVAL s1 20 //cutscenes break if units die who shouldn't: get Lilina to 20
SET_HP Lilina 
SVAL s1 37 //Eliwood's base HP
SET_HP Eliwood
//As Barth is meant to die, he can keep his current HP
CAM1 [11,2] //far enough to push X=0 off-screen
MUSC M_AnUnexpectedCaller
FADU 10
STAL 10
//SVAL s3 Fire
//GIVEITEMTO Lilina //emergency bugfix: game freezes if she counters unarmed
//SVAL s3 IronLance
//GIVEITEMTO Barth //same with him - if you toss the item, nothing I can do...
FlashCursor(Barth,60)
Text(Fort_2_Burning,Ch11EndingMarthGoAheadOfMe)
STAL 30
MOVE 8 Marth [6,4]
ENUN
STAL 15
MOVE 10 Marth [0,4]
MOVE 10 Sheeda [0,5]
MOVE 10 Chiki [1,4]
//they should all be off-screen so no need to clear
ENUN
MOVE 10 Alear [12,2]
ENUN
FlashCursor(Eliwood,60)
Text(Fort_2_Burning,Ch11EndingTheSacrifice)
MOVE 10 Alear [9,1]
ENUN
//Battles
StartBattle
NormalDamage(1,19) //20 is an exact kill at base level, Lilina's meant to live
MissedAttack(0,0)
EndAttack
FIGHT Lilina Alear Elfire 0
STAL 30
//quick line
MOVE 10 Eliwood [10,1]
ENUN
StartBattle
NormalDamage(0,10)
NormalDamage(1,15)
EndAttack
FIGHT Eliwood Alear SilverSword 0
STAL 30
Text(Fort_2_Burning,Ch11EndingAfterScriptedBattles)
MOVE 6 Lilina [0,4]
MOVE 8 Eliwood [1,4]
STAL 20
MOVE 10 Alear [9,2]
ENUN
StartBattle
NormalDamage(1,12)
NormalDamage(0,8)
CriticalHit(1,36)
EndAttack
FIGHT Barth Alear IronLance 0
STAL 15
KILL Barth
AWAIT_KILL Barth
STAL 60
//DISA Barth
//rest in peace, general
FADI 5
CLEA 0
CLEE 0
CLEN 0
GOTO 1121

LABEL 1120
//If you managed to get Barth killed, he can't sacrifice himself
//So Lilina and Eliwood die!
LOAD2 0x0 CutsceneEndingInsideNoBarth
SVAL s1 20 //cutscenes break if units die who shouldn't: get Lilina to 20
SET_HP Lilina 
SVAL s1 37 //Eliwood's base HP
SET_HP Eliwood
//Let Alear kill them if you've gotten Barth killed, but keep as much the same as possible
CAM1 [11,2] //far enough to push X=0 off-screen
MUSC M_AnUnexpectedCaller
FADU 10
STAL 10
FlashCursor(Marth,60)
Text(Fort_2_Burning,PlaceholderText)
STAL 30
MOVE 8 Marth [6,4]
ENUN
STAL 15
MOVE 10 Marth [0,4]
MOVE 10 Sheeda [0,5]
MOVE 10 Chiki [1,4]
//they should all be off-screen so no need to clear
ENUN
MOVE 10 Alear [12,2]
ENUN
FlashCursor(Eliwood,60)
Text(Fort_2_Burning,Ch11EndingTheSacrifice)
MOVE 10 Alear [9,1]
ENUN
//Battles
StartBattle
NormalDamage(1,19) //20 is an exact kill at base level, Lilina's meant to live
MissedAttack(0,0)
EndAttack
FIGHT Lilina Alear Elfire 0
STAL 30
//quick line
MOVE 10 Eliwood [10,1]
ENUN
StartBattle
NormalDamage(0,10)
NormalDamage(1,15)
EndAttack
FIGHT Eliwood Alear SilverSword 0
STAL 30
Text(Fort_2_Burning,PlaceholderText)
MOVE 6 Lilina [5,4]
MOVE 8 Eliwood [6,4]
STAL 20
MOVE 10 Alear [7,4]
ENUN
CAM1 Lilina
StartBattle
NormalDamage(1,15)
NormalDamage(0,10)
CriticalHit(1,45)
EndAttack
FIGHT Eliwood Alear SilverSword 0
STAL 15
KILL Eliwood
AWAIT_KILL Eliwood
STAL 60
MOVE 6 Lilina [3,4]
STAL 10
MOVE 10 Alear [4,4]
ENUN
StartBattle
CriticalHit(1,57)
EndAttack
FIGHT Lilina Alear Elfire 0
STAL 15
KILL Lilina
AWAIT_KILL Lilina
STAL 60
FADI 5
CLEA 0
CLEE 0
CLEN 0
GOTO 1107 //skip to save screen

LABEL 1121
LOMA 0x45 //Durandal cave
Text(Fort_Ruins_Outside,Ch11EndingMarthAtTheCave)
CAM1 [23,22]
LOAD2 0x2 CutsceneEndingDurandal
STAL 30
FlashCursor(Lilina,60)
Text(Ch11EndingToTheDurandal)
CLEAN //text persists otherwise
STAL 10
EVBIT_T 9 //follow
MOVE 10 Lilina [3,4]
MOVE 10 Eliwood [2,5]
//MOVE 10 Marth [3,5]
//MOVE 10 Sheeda [4,5]
//MOVE 10 Chiki [3,6]
//Can only move four units at once, commenting out Akaneians as they're not in next cutscene
ENUN
EVBIT_F 9 //stop following
//This scene's been cut out entirely
//LOAD2 0x2 CutsceneEndingBad
//CAM1 Alear
//MUSC M_Stratagem
//FlashCursor(Framme,60)
//Text(Volcano,Ch11EndingTwistyLittlePassages)
CAM1 Lilina
FlashCursor(Lilina,60)
CHECK_EVENTID DiamantDead
BEQ 1105 s0 sC //if alive, branch
MUSC M_ThemeOfLove
Text(Volcano,Ch11EndingWeGotRoysRing)
Text(Volcano,Ch11EndingDestinationBadon)
GOTO 1106
LABEL 1105
Text(Volcano,Ch11EndingOurBoyIsInAnotherCastle)
LABEL 1106
//If Eliwood's dead no Durandal for you!
//Factor in his death later in-story: Alear has him dead by now at latest
//Actually accounting for this: *way* later, get the everyone lives script done first!
CHECK_ALIVE Eliwood
BEQ 1107 s0 sC
SVAL s3 Durandal
GIVEITEMTO Eliwood
LABEL 1107

WM_LOADLOCATION2 0 10 0 //Required to fix chapter name
WM_SETDESTINATION 0 10 0 //World map node. Keep these equal.
WM_SETNEXTLOCATION 0 0 10 //Needed for world map narration.
MNCH 0xC
ENDA

AfterBattlePrep:
//Wendy quickly says hi
LOAD2 0x0 CutsceneWendyT1
MOVE 10 Wendy [5,2]
ENUN
Text(Ch11AfterPrepSideGates)
MOVE 10 Wendy [20,6]
ENUN
DISA Wendy
TurnAlly(Barth)
NoFade
ENDA

InfiniteRangeBlazingLion:
CAM1 Diamant
STAL 10
FlashCursor(Diamant,60)
Text(Ch11DiamantBurnItAllDown)
STAL 10
WEATHER 5 //fire
EARTHQUAKE 0 1 //horizontal, sound on
STAL 10
CAM1 [16,7]
STAL 10
TILECHANGE 0 //switch on the "fire tiles"
STAL 10
EARTHQUAKE_END
NoFade
ENDA

LoadSouthernReinforcements:
CAM1 [7,25]
LOAD1 0x1 SouthernReinforcements
NoFade
ENDA

LoadSouthernReinforcements2:
CAM1 [7,25]
LOAD1 0x1 SouthernReinforcements2
NoFade
ENDA

LoadDragonKnights:
CAM1 [20,16]
LOAD1 0x1 RogueFlyersEast
CAM1 [7,25]
LOAD1 0x1 RogueFlyersSouth
NoFade
ENDA

WorthlessArsonist:
CAM1 Marth
FlashCursor(Marth,60)
Text(Ch11WhatTheHellDiamant)
NoFade
ENDA

DownGoTheSideGates:
EVBIT_T 9
LOAD1 0x1 WendyFriendsAndFoesWest
STAL 20
LOAD1 0x1 WendyFriendsAndFoesEast
STAL 10
FlashCursor(Wendy,60)
Text(Ch11TheSideGatesFall)
NoFade
ENDA

DiamantsCharge:
CAM1 Diamant
STAL 10
LOAD1 0x1 CutsceneAlear
FlashCursor(Alear,60)
Text(Ch11AlearArrives)
STAL 10
MOVE 10 Alear [7,28]
ENUN
STAL 10
DISA Alear
SVAL s1 0x00000000
CHAI Diamant
CHAI Brodia
//Pursue Without Heed on Diamant and his soldiers
NoFade
ENDA

Village1515:
Text(Village,Ch11VillageText4)
SVAL s3 Hammer
GIVEITEMTO 0xFFFF
NoFade
ENDA

Village1915:
Text(Village,Ch11VillageText2)
SVAL s3 KillerBow
GIVEITEMTO 0xFFFF
NoFade
ENDA

Village118:
Text(Village,Ch11VillageText3)
SVAL s3 AngelicRobe
GIVEITEMTO 0xFFFF
NoFade
ENDA

Village1318:
//Text(Village,Ch11VillageText4)
//SVAL s3 ShortSpear
//GIVEITEMTO 0xFFFF
MUSC M_JoinUs
Text(Village,Ch11AstoreLurks)
LOAD1 0x1 AstoreGroup
NoFade
ENDA

Village1521:
Text(Village,Ch11VillageText5)
SVAL s3 BrightLance
GIVEITEMTO 0xFFFF
NoFade
ENDA

Village1921:
Text(Village,Ch11VillageText6)
SVAL s3 Physic
GIVEITEMTO 0xFFFF
NoFade
ENDA

Village124:
Text(Village,Ch11VillageText7)
SVAL s3 EnergyRing
GIVEITEMTO 0xFFFF
NoFade
ENDA

Village1324:
Text(Village,Ch11VillageText8)
SVAL s3 Swordslayer
GIVEITEMTO 0xFFFF
NoFade
ENDA

Ch11Vendor:
SHLI Fire Elfire Lightning Shine Flux Heal Vulnerary
ALIGN 4

Ch11WestArmoury:
SHLI IronSword SteelSword Armourslayer IronLance SteelLance Javelin
ALIGN 4

Ch11EastArmoury:
SHLI IronAxe SteelAxe Swordreaver IronBow SteelBow ShortBow
ALIGN 4

AlearT9REDA:
REDA [7,25]

SouthREDA1:
REDA [5,27]

SouthREDA2:
REDA [6,27]

SouthREDA3:
REDA [7,27]

WendyREDA:
REDA [17,6]

WendyEastAllyREDA:
REDA [15,6]

WendyEastEnemyREDA1:
REDA [19,6]

WendyEastEnemyREDA2:
REDA [20,5]

WendyWestAllyREDA1:
REDA [3,5]

WendyWestAllyREDA2:
REDA [3,6]

WendyWestEnemyREDA1:
REDA [2,6]

WendyWestEnemyREDA2:
REDA [1,4]

DragonREDA1:
REDA [17,25]

DragonREDA2:
REDA [20,16]

DragonREDA3:
REDA [5,26]

DragonREDA4:
REDA [9,26]

MarthREDA:
REDA [24,22]

SheedaREDA:
REDA [25,21]

ChikiREDA:
REDA [25,23]

EliwoodREDA:
REDA [22,21]

LilinaREDA:
REDA [24,20]

AstoreREDA:
REDA [13,19]
