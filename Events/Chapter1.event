EventPointerTable(0x7, PointerList)

ALIGN 4
PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Good Good  //This unit group determines the number and position of deployment slots when there is a prep screen
POIN $0 $0 $0
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0,Turn2ActivateGacha,2)
TurnEventPlayer(0,KrisArrives,10)
END_MAIN

CharacterBasedEvents:
CharacterEventBothWays(0x8,TheHighestBidder,Marth,Caesar)
END_MAIN

LocationBasedEvents:
Vendor(Ch1Vendor,12,7)
Armory(Ch1Armoury,14,6)
House(0,Ch1House1,13,5)
House(0,Ch1House2,12,6)
House(0,Ch1House3,13,6)
House(0,Ch1House4,14,7)
SecretShop(Ch1Secret,15,4)
#ifdef __DEBUG__
Seize(0x3,EndingScene,13,8)
Seize(0x3,DebugWarp,13,7)
#endif
END_MAIN

MiscBasedEvents:
DefeatBoss(EndingScene)
CauseGameOverIfLordDies
END_MAIN

Dunno1:
END_MAIN

Dunno2:
END_MAIN

Dunno3:
END_MAIN

Tutorial:
END_MAIN

Good:
UNIT Marth Lord 0x00 Level(1,Ally,0) [16,10] 0x0 0x0 0x1 MarthREDA [Falchion, MemberCard, Vulnerary] NoAI
UNIT

GoodJam:
UNIT Marth StarLord 0x00 Level(1,Ally,0) [16,10] 0x0 0x0 0x1 MarthREDA [Falchion, WhiteGem, Vulnerary] NoAI
UNIT


CutsceneMarth:
UNIT Marth Lord 0x00 Level(1,Ally,0) [2,1] 0x0 0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI
UNIT

Bad:
UNIT KrisFaction Fleet   Caesar Level(1,Enemy,1) [12,0] 0x0 0x0 0x1 REDA6 [Ballista] NeverMoveAI
UNIT KrisFaction Fighter Caesar Level(1,Enemy,1) [1,0] 0x0 0x0 0x1 REDA1 [IronAxe] AttackInRangeAI
UNIT KrisFaction Fighter Caesar Level(1,Enemy,1) [1,0] 0x0 0x0 0x1 REDA2 [IronAxe] AttackInRangeAI
UNIT KrisFaction Archer  Caesar Level(1,Enemy,1) [1,0] 0x0 0x0 0x1 REDA3 [IronBow] AttackInRangeAI
UNIT KrisFaction ArmourKnight Caesar Level(1,Enemy,1) [1,0] 0x0 0x0 0x1 REDA4 [IronLance] AttackInRangeAI
UNIT KrisFaction PegasusKnight Caesar Level(1,Enemy,1) [3,10] 0x0 0x0 0x1 REDA5 [IronLance] PursueWithoutHeedAI
UNIT


BadJam:
UNIT KrisFaction Fleet   Lucina Level(10,Enemy,1) [12,0] 0x0 0x0 0x1 REDA6 [Ballista] NeverMoveAI
UNIT KrisFaction Warrior Lucina Level(1,Enemy,1) [1,0] 0x0 0x0 0x1 REDA1 [IronAxe] AttackInRangeAI
UNIT KrisFaction Warrior Lucina Level(1,Enemy,1) [1,0] 0x0 0x0 0x1 REDA2 [IronAxe] AttackInRangeAI
UNIT KrisFaction Sniper  Lucina Level(1,Enemy,1) [1,0] 0x0 0x0 0x1 REDA3 [IronBow] AttackInRangeAI
UNIT KrisFaction General Lucina Level(1,Enemy,1) [1,0] 0x0 0x0 0x1 REDA4 [IronLance] AttackInRangeAI
UNIT KrisFaction PegasusKnight Lucina Level(15,Enemy,1) [3,10] 0x0 0x0 0x1 REDA5 [IronLance] PursueWithoutHeedAI
UNIT

LucinaGroup:
UNIT Lucina Swordmaster_F 0x00 Level(5,Enemy,0) [1,0] 0x0 0x0 0x0 0x0 [SilverSword, LightBrand] AttackInRangeAI
UNIT

CaesarGroup:
UNIT Caesar Mercenary 0x00 Level(5,Enemy,0) [1,0] 0x0 0x0 0x0 0x0 [SteelSword] AttackInRangeAI
UNIT

TooLate:
UNIT Kris Hero 0x00 Level(20,Enemy,0) [1,0] 0x00 0x00 0x1 KrisREDA [MiracleSword] PursueWithoutHeedAI
UNIT

MarthAlearGroup:
UNIT Marth Lord 0x00 Level(1,Ally,0) [6,5] 0x0  0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI
UNIT Alear Swordmaster_F 0x00 Level(1,Ally,0) [8,5] 0x0  0x0 0x0 0x0 [Rapier] NoAI
UNIT

RoyalIntroGroup:
UNIT Marth Lord 0x00 Level(1,Ally,0) [7,5] 0x0  0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI
UNIT Vander KnightPaladin 0x00 Level(1,NPC,0) [7,6] 0x0  0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI //Vander
UNIT 0x81 KnightPaladin 0x00 Level(1,NPC,0) [2,5] 0x0  0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI //Alfred
UNIT Celine Sage_F 0x00 Level(1,NPC,0) [3,6] 0x0  0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI //Celine
UNIT Diamant Hero 0x00 Level(1,NPC,0) [1,2] 0x0  0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI //Diamant
UNIT Alcryst Sniper 0x00 Level(1,NPC,0) [2,1] 0x0  0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI //Alcryst
UNIT Ivy Sage_F /*DragonKnight_F*/ 0x00 Level(1,NPC,0) [12,1] 0x0  0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI //Ivy
UNIT Hortensia Bishop_F /*FalcoKnight*/ 0x00 Level(1,NPC,0) [13,2] 0x0  0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI //Hortensia
UNIT 0x87 General 0x00 Level(1,NPC,0) [12,5] 0x0  0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI //Timerra
UNIT 0x88 Sniper /*Horseman*/ 0x00 Level(1,NPC,0) [11,6] 0x0  0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI //Fogado
UNIT Veyle Druid_F 0x00 Level(1,NPC,0) [7,8] 0x0  0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI //Veyle
UNIT

WeddingGroup:
UNIT Marth Lord 0x00 Level(1,Ally,0) [8,5] 0x0  0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI
UNIT Alear Swordmaster_F 0x00 Level(1,Ally,0) [6,5] 0x0  0x0 0x0 0x0 [Rapier] NoAI
UNIT Vander KnightPaladin 0x00 Level(1,NPC,0) [7,6] 0x0  0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI //Vander
UNIT 0x81 KnightPaladin 0x00 Level(1,NPC,0) [5,6] 0x0  0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI //Alfred, dies and unfought
UNIT Celine Sage_F 0x00 Level(1,NPC,0) [4,6] 0x0  0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI //Celine
UNIT Diamant Hero 0x00 Level(1,NPC,0) [2,6] 0x0  0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI //Diamant
UNIT Alcryst Sniper 0x00 Level(1,NPC,0) [1,6] 0x0  0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI //Alcryst
UNIT Ivy Sage_F /*DragonKnight_F*/ 0x00 Level(1,NPC,0) [12,6] 0x0  0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI //Ivy
UNIT Hortensia Bishop_F /*FalcoKnight*/ 0x00 Level(1,NPC,0) [13,6] 0x0  0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI //Hortensia
UNIT 0x87 General 0x00 Level(1,NPC,0) [9,6] 0x0  0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI //Timerra, unfought
UNIT 0x88 Sniper /*Horseman*/ 0x00 Level(1,NPC,0) [10,6] 0x0  0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI //Fogado
UNIT Boucheron Fighter 0x00 Level(1,NPC,0) [5,7] 0x0  0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI //Boucheron
UNIT 0x8B KnightFalcon 0x00 Level(1,NPC,0) [4,7] 0x0  0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI //Chloe
UNIT 0x8C General 0x00 Level(1,NPC,0) [2,7] 0x0  0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI //Jade
UNIT Lapis Swordmaster_F 0x00 Level(1,NPC,0) [1,7] 0x0  0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI //Lapis
UNIT 0x8E Rogue 0x00 Level(1,NPC,0) [12,7] 0x0  0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI //Zelkov
UNIT 0x8F KnightDragon 0x00 Level(1,NPC,0) [13,7] 0x0  0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI //Rosado
UNIT 0x90 Gwyllgi 0x00 Level(1,NPC,0) [9,7] 0x0  0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI //Merrin
UNIT 0x91 Bishop 0x00 Level(1,NPC,0) [10,7] 0x0  0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI //Pandreo
UNIT Veyle Druid_F 0x00 Level(1,NPC,0) [5,5] 0x0  0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI //Veyle
UNIT Clanne Sage 0x00 Level(1,NPC,0) [2,2] 0x0  0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI //Clanne
UNIT Framme Bishop_F 0x00 Level(1,NPC,0) [12,2] 0x0  0x0 0x0 0x0 [Falchion, WhiteGem, Vulnerary] NoAI //Framme
UNIT

MarthBoat:
UNIT Akaneia Fleet   Marth Level(1,NPC,1) [19,10] 0x0 0x0 0x1 BoatREDA [Ballista] NeverMoveAI
UNIT


Traps1:
SavePoint(15,5)
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

BeginningScene:
//Intro Setup
SetBackground(CGBlackScreen)
EVBIT_MODIFY 1 //No skipping
TUTORIALTEXTBOXSTART
CenterTutorialTextBox
TEXTSHOW HackIntro
TEXTEND
STAL 10
SVAL s1 1
BEQ 1 sC s1 //branch if yes

TUTORIALTEXTBOXSTART
CenterTutorialTextBox
EVBIT_MODIFY 0 //skip away
TEXTSHOW JamModeNo
TEXTEND
FADI 60
STAL 1

//Opening

MUSC M_ThemeOfLove
LOMA 26
LOAD2 0x1 MarthAlearGroup
CALL $9EE2C4 //EndConvo, get rid of black screen
//FADU 10
BROWNBOX LocationSomniel [0,0]
FlashCursor(Marth,60)
Text(Castle_Hall_Night,Ch1TheNightBefore)
FADI 10
CLEA 0
LOAD2 0x1 RoyalIntroGroup
MUSC M_Ricardo
FADU 10
BROWNBOX LocationNextMorning [0,0]
FlashCursor(Vander,60)
Text(Castle_Room,Ch1RoyalIntroductions)
MOVE 8 Vander [13,1]
STAL 10
MOVE 8 0x81 [7,6]
MOVE 8 Celine [6,6]
ENUN
Text(Castle_Room,Ch1RoyalIntroductionsFirene)
MOVE 8 0x81 [0,5]
MOVE 8 Celine [0,6]
STAL 10
MOVE 8 Diamant [7,6]
MOVE 8 Alcryst [6,6]
ENUN
Text(Castle_Room,Ch1RoyalIntroductionsBrodia)
MOVE 8 Diamant [1,0]
MOVE 8 Alcryst [2,0]
STAL 10
MOVE 8 0x81 [4,6]
MOVE 8 Celine [5,6]
MOVE 8 Ivy [7,6]
MOVE 8 Hortensia [8,6]
ENUN
Text(Castle_Room,Ch1RoyalIntroductionsElusia)
MOVE 8 Ivy [14,5]
MOVE 8 Hortensia [14,6]
STAL 10
MOVE 8 Diamant [1,5]
MOVE 8 Alcryst [2,5]
MOVE 8 0x87 [7,6]
MOVE 8 0x88 [8,6]
ENUN
Text(Castle_Room,Ch1RoyalIntroductionsSolm)
MOVE 8 0x87 [11,4]
MOVE 8 0x88 [11,5]
STAL 10
MOVE 8 Ivy [12,4]
MOVE 8 Hortensia [12,5]
MOVE 8 Veyle [7,6]
ENUN
Text(Castle_Room,Ch1RoyalIntroductionsVeyle)
MOVE 8 Marth [1,0]
MOVE 8 Veyle [1,1]
ENUN
STAL 10
FADI 10
STAL 10
CLEA 0
CLEN 0
MUSC M_ThemeOfLove
LOAD2 0x1 WeddingGroup
STAL 10
FADU 10
STAL 10
FlashCursor(Vander,60)
STAL 10
Text(Shrine_lighter,Ch1AtTheAltar)
MUSC M_OffToWar
Text(Shrine_lighter,Ch1IObject)
TurnEnemy(Alear)
TurnEnemy(Clanne)
TurnEnemy(Vander)
TurnEnemy(0x81) //Alfred
TurnEnemy(Celine)
TurnEnemy(Diamant)
TurnEnemy(Alcryst)
TurnEnemy(Ivy)
TurnEnemy(Hortensia)
TurnEnemy(0x87) //Timerra
TurnEnemy(0x88) //Fogado
TurnEnemy(Veyle) 
TurnEnemy(Boucheron)
TurnEnemy(0x8B) //Chloe
TurnEnemy(0x8C) //Jade
TurnEnemy(Lapis)
TurnEnemy(0x8E) //Zelkov
TurnEnemy(0x8F) //Rosado
TurnEnemy(0x90) //Merrin
TurnEnemy(0x91) //Pandreo
TurnEnemy(Framme)
MOVE 8 Marth [7,10]
ENUN
Text(Ch1AlearLeftAtAltar)
FADI 10
CLEA 0
CLEN 0
CLEE 0
REMA
LOMA 0
FADU 10
LOAD1 0x1 MarthBoat
ENUN
LOAD1 0x1 Good
BROWNBOX LocationPortWarren [0,0]
MOVE 8 Marth [13,6]
ENUN
Text(Inn,Ch1OpeningBackInWarren)
MOVE 8 Marth [15,4]
ENUN
Text(HouseBG,Ch1OpeningSecretShopNew)
LOAD1 0x1 CaesarGroup
CAM1 Caesar
FlashCursor(Caesar,60)
Text(Ch1OpeningOrderAttackNew)
LOAD1 0x1 Bad
ENUN
CriticalHit(0,30)
EndAttack
FIGHT_MAP KrisFaction Akaneia Ballista 0
//the zero's apparently the ballista flag? febuilder just takes it as part of the item
//KILL Akaneia
//AWAIT_KILL Akaneia
MOVE 8 KrisFaction [12,1]
Text(Ch1OpeningMarthFights)
MOVE 10 Marth [13,8]
ENUN
CLEN 0
SVAL s3 5000
GIVEMONEY Marth //I'd prefer this to be silent but it's better than selling a blue gem turn 1
NoFade
ENDA

LABEL 1
TUTORIALTEXTBOXSTART
CenterTutorialTextBox
EVBIT_MODIFY 0
ENUT JamModeOn
TEXTSHOW JamModeYes
TEXTEND
FADI 60
STAL 1

//Jam Mode Opening

MUSC M_ThemeOfLove
LOMA 26 //just use the somniel here, it was always implied
CALL $9EE2C4 //EndConvo
Text(Castle_Hall,Ch1OpeningBeforeAltar)
Text(Castle_Hall,Ch1OpeningAnnaVisits)
Text(Shrine_lighter,Ch1OpeningAltar)
MUSC M_OffToWar
Text(Shrine_lighter,Ch1OpeningIObject)
FADI 10
LOMA 0 //back to warren
LOAD1 0x1 GoodJam
FADU 10
Text(Inn,Ch1OpeningBackInWarren)
Text(HouseBG,Ch1OpeningSecretShop)
LOAD1 0x1 BadJam
LOAD1 0x1 LucinaGroup
Text(Ch1OpeningOrderAttack)
Text(Ch1OpeningMarthFights)
NoFade
ENDA

EndingScene:
FADI 10
CLEA 0
CLEE 0
CLEN 0
REMA
LOAD2 0x1 CutsceneMarth
FADU 10
MUSC M_TogetherWeRide
CHECK_EVENTID JamModeOn
BNE 2 sC s0 //branch if jam mode 
CALL UnitGachaAct1
WARPIN [1,0]
MOVE 0xFFFF 0xFFFD [1,0]
ENUN
ENDWARP
STAL 20
MUSC M_Victory
Text(Plain,Ch1EndingNew)
GOTO 3
LABEL 2
//Jam Mode
CALL UnitGachaJam
WARPIN [1,0]
MOVE 0xFFFF 0xFFFD [1,0]
ENUN
ENDWARP
STAL 20
MUSC M_Victory
Text(Plain,Ch1Ending)
LABEL 3
WM_LOADLOCATION2 0 1 0
WM_SETDESTINATION 0 1 0 //World map node. Keep these equal.
WM_SETNEXTLOCATION 0 0 1 //Needed for world map narration.
MNCH 0x1
ENDA

Turn2ActivateGacha:
Text(HouseBG,Ch1AnnaSummonsAnArmy)
CAM1 [17,7]
STAL 10
CHECK_EVENTID JamModeOn
BNE 8 sC s0 //branch if jam mode 
CALL UnitGachaAct1
WARPIN [16,7]
MOVE 0xFFFF 0xFFFD [16,7]
ENUN
ENDWARP
STAL 30

CALL UnitGachaAct1
WARPIN [16,6]
MOVE 0xFFFF 0xFFFD [16,6]
ENUN
ENDWARP
STAL 30

CALL UnitGachaAct1
WARPIN [16,8]
MOVE 0xFFFF 0xFFFD [16,8]
ENUN
ENDWARP
STAL 30

CALL UnitGachaAct1
WARPIN [15,7]
MOVE 0xFFFF 0xFFFD [15,7]
ENUN
ENDWARP
STAL 30

CALL UnitGachaAct1
WARPIN [17,7]
MOVE 0xFFFF 0xFFFD [17,7]
ENUN
ENDWARP
STAL 30

GOTO 9
LABEL 8
CALL UnitGachaJam
WARPIN [16,7]
MOVE 0xFFFF 0xFFFD [16,7]
ENUN
ENDWARP
STAL 30

CALL UnitGachaJam
WARPIN [16,6]
MOVE 0xFFFF 0xFFFD [16,6]
ENUN
ENDWARP
STAL 30

CALL UnitGachaJam
WARPIN [16,8]
MOVE 0xFFFF 0xFFFD [16,8]
ENUN
ENDWARP
STAL 30

CALL UnitGachaJam
WARPIN [15,7]
MOVE 0xFFFF 0xFFFD [15,7]
ENUN
ENDWARP
STAL 30

CALL UnitGachaJam
WARPIN [17,7]
MOVE 0xFFFF 0xFFFD [17,7]
ENUN
ENDWARP
STAL 30

LABEL 9
Text(HouseBG,Ch1AfterSummoning)
NoFade
ENDA

TheHighestBidder:
//Hiring Caesar - idea for later patch: secret event where Citrinne buys him off
MUSC M_Conspiracy
TEXTSTART
TEXTSHOW Ch1BuyingCaesarOff
TEXTEND
SVAL s1 5000
CHECK_MONEY
BGT 110 s1 sC //can't afford him, no good
TEXTSTART
TEXTSHOW Ch1HireCaesarYesNo
TEXTEND
REMA
SVAL s7 1 //it's how VBA Ch24 does it, I'm stealing the conditional from there
BNE 111 s7 sC
SVAL s3 5000
TAKEMONEY
TurnAlly(Caesar)
MUSC M_TogetherWeRide
ENUT 0x3 //chapter completed
Text(Ch1CaesarGet)
STAL 10
SET_ACTIVE 0 //should stop Marth from running in place
MOVE 10 Marth [1,0]
MOVE 10 Caesar [1,0]
ENUN
WM_LOADLOCATION2 0 1 0
WM_SETDESTINATION 0 1 0 //World map node. Keep these equal.
WM_SETNEXTLOCATION 0 0 1 //Needed for world map narration.
MNCH 0x1
GOTO 112
LABEL 111
Text(Ch1EtTuMarth)
GOTO 112
LABEL 110
REMA
Text(Ch1TimeWasting)
LABEL 112
NoFade
ENDA

Ch1House1:
Text(HouseBG,Ch1House1Text)
NoFade
ENDA

Ch1House2:
Text(HouseBG,Ch1House2Text)
NoFade
ENDA

Ch1House3:
Text(HouseBG,Ch1House3Text)
NoFade
ENDA

Ch1House4:
Text(HouseBG,Ch1House4Text)
NoFade
ENDA

KrisArrives:
MUSC M_Conspiracy
LOAD1 0x1 TooLate
FlashCursor(Kris,60)
Text(Ch1KrisArrives)
NoFade
ENDA

DebugWarp:
MNC2 0x3C //go to debug map
ENDA

KrisREDA:
REDA [1,0]

REDA1:
REDA [8,6]

REDA2:
REDA [4,5]

REDA3:
REDA [5,4]

REDA4:
REDA [3,2]

REDA5:
REDA [3,9]

REDA6:
REDA [16,1]

BoatREDA:
REDA [16,10]

MarthREDA:
REDA [13,8]

Ch1Armoury:
SHLI IronSword SteelSword IronLance SteelLance IronAxe SteelAxe IronBow SteelBow
ALIGN 4

Ch1Vendor:
SHLI Fire Thunder Wind Lightning Flux Heal Mend Vulnerary PureWater
ALIGN 4

Ch1Secret:
//your reward for paying attention in the cutscene: stuff you can't buy!
SHLI Talisman ElysianWhip Doulam WhiteGem Meteor Fortify Warp Boots
ALIGN 4
